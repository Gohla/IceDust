module Model

imports

  Common
  Expressions
  Types
  
context-free syntax

  Model.Entity = <
    entity <ID> {
      <{Member "\n"}*>
    }
  >
  
  Model.FirstClassRelation = <
    relation <ID> {
      <{Member "\n"}*>
    }
  >
  
  Model.Relation = [
    relation [ID][DotID?] [Multiplicity?] [Ordering?] <-> [Multiplicity?] [Ordering?] [ID][DotID?]
  ]
  
  Model.ShortcutRelation = [
    relation [ID][DotID?] [Multiplicity?] [Ordering?] <-> [Multiplicity?] [Ordering?] [ID][DotID?] {
      [{Member "\n"}*]
    }
  ]
  
  Member.Attribute = <
    <ID> : <Type> <Multiplicity?>
  >
  
  Member.DerivationAttribute = <
    <ID> : <Type> <Multiplicity?> = <Exp>
  >
  
  Member.DefaultAttribute = <
    <ID> : <Type> <Multiplicity?> = <Exp> (default)
  >

  Member.Role = [
    [ID][DotID?] [Multiplicity?][Ordering?][SpaceArrowID?]
  ]

  Member.Shortcut = [
    [ID][DotID?] <-> [ID][DotID?]
  ]
  
  DotID        = <.<ID>>
  SpaceArrowID = [ -> [ID]]
