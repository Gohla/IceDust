module Model

imports

  Common
  Expressions
  Types
  
context-free syntax

  Model.Entity = <
    entity <ID> {
      <{Member "\n"}*>
    }
  >
  
  Model.Relation = [
    relation [ID].[ID] [Multiplicity?] [Ordering?] <-> [Multiplicity?] [Ordering?] [ID].[ID]
  ]
  
  Model.FirstClassRelation = <
    relation <ID> {
      <{Member "\n"}*>
    }
  >
  
  Model.ShortcutRelation = [
    relation [ID].[ID] [Multiplicity?] [Ordering?] <-> [Multiplicity?] [Ordering?] [ID].[ID] {
      [{Member "\n"}*]
    }
  ]
  
  Member.Attribute = <
    <ID> : <Type> <Multiplicity?>
  >
  
  Member.DerivationAttribute = <
    <ID> : <Type> <Multiplicity?> = <Exp>
  >
  
  Member.DefaultAttribute = <
    <ID> : <Type> <Multiplicity?> = <Exp> (default)
  >

  Member.Role = [
    [ID][DotID?] [Multiplicity][Ordering?][SpaceArrowID?]
  ]
  DotID        = <.<ID>>
  SpaceArrowID = [ -> [ID]]

  Member.Shortcut = [
    [ID].[ID] <-> [ID].[ID]
  ]
  