module Model

imports

  Common
  Expressions
  Types
  
context-free syntax // entities

  Model.Entity = <
    entity <ID> <ExtendsExtends?>{
      <{Member "\n"}*>
    }
  >
  
  Model.FirstClassRelation = <
    relation <ID> <ExtendsExtends?>{
      <{Member "\n"}*>
    }
  >
  
  Extends.Extends = <<ID>>

context-free syntax // relations

  Model.Relation = [
    relation [ID][DotID?][SpaceMultiplicity?][SpaceOrdering?] <->[SpaceMultiplicity?][SpaceOrdering?] [ID][DotID?]
  ]

context-free syntax // sugar: binary relation with attributes 

  Model.ShortcutRelation = [
    relation [ID][DotID?][SpaceMultiplicity?][SpaceOrdering?] <->[SpaceMultiplicity?][SpaceOrdering?] [ID][DotID?] {
      [{Member "\n"}*]
    }
  ]

context-free syntax // entity members

  Member.Attribute = <
    <ID> : <Type><Multiplicity?>
  >
  
  Member.DerivationAttribute = <
    <ID> : <Type><Multiplicity?> = <Exp>
  >
  
  Member.DefaultAttribute = <
    <ID> : <Type><Multiplicity?> = <Exp> (default)
  >

  Member.Role = [
    [ID][DotID?] [Multiplicity?][Ordering?][SpaceArrowID?]
  ]

  Member.Shortcut = [
    [ID][DotID?] <-> [ID][DotID?]
  ]
  
context-free syntax // pretty printer hacks
  
  DotID             = <.<ID>>
  SpaceArrowID      = [ -> [ID]]
  SpaceMultiplicity = [ [Multiplicity]]
  SpaceOrdering     = [ [Ordering]]
  ExtendsExtends    = [extends [{Extends ", "}+] ]