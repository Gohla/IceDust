module Model

imports

  Common
  Expressions
  Types
  
context-free syntax // entities

  Model.Entity = <
    entity <ID> <OptionalExtends?>{
      <{Member "\n"}*>
    }
  >
  
  Model.FirstClassRelation = <
    relation <ID> <OptionalExtends?>{
      <{Member "\n"}*>
    }
  >

  Model.Trait = <
    trait <ID> <OptionalExtends?>{
      <{Member "\n"}*>
    }
  >
  
  OptionalExtends.OptionalExtends = [extends [{Extends ", "}+] ] // need a constructor to dispatch on in NaBL2
  
  Extends.Extends = <<ID>>

context-free syntax // relations

  Model.Relation = [
    relation [ID][DotID?][SpaceMultiplicity?][SpaceOrdering?] <->[SpaceMultiplicity?][SpaceOrdering?] [ID][DotID?]
  ]

context-free syntax // sugar: binary relation with attributes 

  Model.ShortcutRelation = [
    relation [ID][DotID?][SpaceMultiplicity?][SpaceOrdering?] <->[SpaceMultiplicity?][SpaceOrdering?] [ID][DotID?] {
      [{Member "\n"}*]
    }
  ]

context-free syntax // entity members: attributes

  Member.Attribute = <
    <ID> : <Type><Multiplicity?>
  >
  
  Member.DerivationAttribute = <
    <ID> : <Type><Multiplicity?> = <Exp>
  >
  
  Member.DefaultAttribute = <
    <ID> : <Type><Multiplicity?> = <Exp> (default)
  >

context-free syntax // entity members: roles & inverses

  Member.Role = [
    [ID][DotID?] [Multiplicity?][Ordering?][SpaceArrowID?]
  ]

context-free syntax // entity members: shortcuts

  Member.Shortcut = [
    [ID][DotID?] <-> [ID][DotID?]
  ]
  
context-free syntax // pretty printer hacks
  
  DotID             = <.<ID>>
  SpaceArrowID      = [ -> [ID]]
  SpaceMultiplicity = [ [Multiplicity]]
  SpaceOrdering     = [ [Ordering]]
