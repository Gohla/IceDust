module analysis/_type-functions

imports
 
  static-semantics

signature constructors
  
  LeastUpperBound : Type * Type -> TypeOp

rules

  nabl2-is-op-hook = ?LeastUpperBound(_,_)
  
  nabl2-reduce-op-hook(|sol): LeastUpperBound(a         , a         ) -> a
  nabl2-reduce-op-hook(|sol): LeastUpperBound(a         , TNoValue()) -> a
  nabl2-reduce-op-hook(|sol): LeastUpperBound(TNoValue(), a         ) -> a

signature constructors
  
  IsSubtype : Type * Type -> TypeOp // super type * sub type

rules

  nabl2-is-op-hook = ?IsSubtype(_,_)

  nabl2-reduce-op-hook(|sol): IsSubtype(a            , a         ) -> None()
  nabl2-reduce-op-hook(|sol): IsSubtype(_            , TNoValue()) -> None()
  
signature constructors
  
  IsSubtype : Type * List(Type) -> TypeOp // super type * List(sub type)

rules

  nabl2-is-op-hook = ?IsSubtype(_,_)

  nabl2-reduce-op-hook(|sol): IsSubtype(a, []               ) -> None()
  nabl2-reduce-op-hook(|sol): IsSubtype(a, [a | bs]         ) -> None() where <nabl2-reduce-op-hook(|sol)>IsSubtype(a, bs)
  nabl2-reduce-op-hook(|sol): IsSubtype(a, [TNoValue() | bs]) -> None() where <nabl2-reduce-op-hook(|sol)>IsSubtype(a, bs)
  
signature constructors
  
  IsType : Type -> TypeOp // super type * sub type

rules

  nabl2-is-op-hook = ?IsType(_)

  nabl2-reduce-op-hook(|sol): IsType(TBoolean() ) -> None()
  nabl2-reduce-op-hook(|sol): IsType(TDatetime()) -> None()
  nabl2-reduce-op-hook(|sol): IsType(TFloat()   ) -> None()
  nabl2-reduce-op-hook(|sol): IsType(TInt()     ) -> None()
  nabl2-reduce-op-hook(|sol): IsType(TString()  ) -> None()
  nabl2-reduce-op-hook(|sol): IsType(TNoValue() ) -> None()
  nabl2-reduce-op-hook(|sol): IsType(TEntity(_) ) -> None()
