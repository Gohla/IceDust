module model

imports

  signatures/-

rules

  [[ Entity(e, as) ^ (module_scope) ]] :=
    !D(entity_scope)/Attribute | error "Duplicate attribute" @NAMES,
    Entity{e} <- module_scope,
    entity_scope ---> module_scope,
    Entity{e} ===> entity_scope,
    Entity{e} : TEntity(Entity{e}),
    Implicit{"this"} <- entity_scope,
    Implicit{"this"} : TEntity(Entity{e}),
    Map2 [[ as ^ (entity_scope, module_scope) ]],
    Entity2{e} -> module_scope,   // matches 0 or more times, one for each relation (like partial class definition)
    Entity2{e} <=I= entity_scope, // import all from that reference
    new entity_scope.

  [[ FirstClassRelation(e, as) ^ (module_scope) ]] := // identical to Entity
    !D(entity_scope)/Attribute | error "Duplicate attribute" @NAMES,
    Entity{e} <- module_scope,
    entity_scope ---> module_scope,
    Entity{e} ===> entity_scope,
    Entity{e} : TEntity(Entity{e}),
    Implicit{"this"} <- entity_scope,
    Implicit{"this"} : TEntity(Entity{e}),
    Map2 [[ as ^ (entity_scope, module_scope) ]],
    Entity2{e} -> module_scope,
    Entity2{e} <=I= entity_scope,
    new entity_scope.
    
  [[ Relation(e1, r1, m1, m2, e2, r2) ^ (module_scope) ]] :=
    Entity2{e1} <- module_scope,
    Entity2{e1} ===> entity1_scope,
    Member{r1} <- entity1_scope,
    Entity{e1} -> module_scope,
    Entity{e1} |-> e1_def,
    e1_def : e1_type,
    [[m1 ^ (module_scope) : m1_mult ]],
    Member{r1} : TTuple(e2_type, m1_mult),
    Entity2{e2} <- module_scope,
    Entity2{e2} ===> entity2_scope,
    Member{r2} <- entity2_scope,
    Entity{e2} -> module_scope,
    Entity{e2} |-> e2_def,
    e2_def : e2_type,
    [[m2 ^ (module_scope) : m2_mult ]],
    Member{r2} : TTuple(e1_type, m2_mult),
    new entity1_scope,
    new entity2_scope.
    
  [[ Attribute(a, t, m) ^ (entity_scope, module_scope) ]] :=
    Member{a} <- entity_scope,
    Member{a} : TTuple(a_type, a_mult),
    [[ t ^ (entity_scope) : a_type ]],
    [[ m ^ (entity_scope) : a_mult ]].
    
  [[ DerivationAttribute(a, t, m, e) ^ (entity_scope, module_scope) ]] :=
    Member{a} <- entity_scope,
    Member{a} : TTuple(a_type, a_mult),
    [[ t ^ (entity_scope) : a_type ]],
    [[ m ^ (entity_scope) : a_mult ]],
    [[ e ^ (entity_scope) : TTuple(e_type, e_mult) ]],
    IsSubtype(a_type, e_type) == None() | error $[Type error: expected [a_type] got [e_type].]@e,
    InBounds (a_mult, e_mult) == None() | error $[Multiplicity error: expected [a_mult] got [e_mult].]@e.
    
  [[ DefaultAttribute(a, t, m, e) ^ (entity_scope, module_scope) ]] :=
    Member{a} <- entity_scope,
    Member{a} : TTuple(a_type, a_mult),
    [[ t ^ (entity_scope) : a_type ]],
    [[ m ^ (entity_scope) : a_mult ]],
    [[ e ^ (entity_scope) : TTuple(e_type, e_mult) ]],
    IsSubtype(a_type, e_type) == None() | error $[Type error: expected [a_type] got [e_type].]@e,
    InBounds (a_mult, e_mult) == None() | error $[Multiplicity error: expected [a_mult] got [e_mult].]@e.
    
  [[ Role(entity, inverse, mult, role) ^ (entity_scope, module_scope) ]] :=
    [[ mult ^ (entity_scope) : inverse_mult ]],
    Implicit{"this"} -> entity_scope,
    Implicit{"this"} |-> this_def,
    this_def : this_type,
    Member{role} <- entity_scope,
    Member{role} : TRole(TTuple(entity_type, TOne()), inverse_mult),
    Entity{entity} -> entity_scope,
    Entity{entity} |-> entity_def,
    entity_def : entity_type,
    Entity2{entity} <- module_scope,
    Entity2{entity} ===> entity2_scope,
    Member{inverse} <- entity2_scope,
    Member{inverse} : TTuple(this_type, inverse_mult),
    new entity2_scope,
    Member2{role} -> entity_scope,
    Member2{role} <=I= entity2_scope, // import all shortcut definitions from the role associated scope into the entity2 scope
    true.
    
  [[ Shortcut(role1, shortcut1, role2, shortcut2) ^ (entity_scope, module_scope) ]] :=
    Member{role1} -> entity_scope,
    Member{role1} |-> role1_def,
    role1_def : TRole(TTuple(role1_type, role1_mult), role1_inverse_mult),
    Member2{role1} <- entity_scope,
    Member2{role1} ===> role1_scope,
    Member{shortcut1} <- role1_scope,
    Member{shortcut1} : TShortcut(TTuple(role2_type, role1_inverse_mult), this_type),
    new role1_scope,
    Member{role2} -> entity_scope,
    Member{role2} |-> role2_def,
    role2_def : TRole(TTuple(role2_type, role2_mult), role2_inverse_mult),
    Member2{role2} <- entity_scope,
    Member2{role2} ===> role2_scope,
    Member{shortcut2} <- role2_scope,
    Member{shortcut2} : TShortcut(TTuple(role1_type, role2_inverse_mult), this_type),
    new role2_scope,
    Implicit{"this"} -> entity_scope,
    Implicit{"this"} |-> this_def,
    this_def : this_type,
    true.
   