module students

model
	
	entity Student {
		name         : String
	}
	
	entity Course {
		name         : String
		averageGrade : Float? = avg(enrollment.grade)
		passPerc     : Int = 100 * sum(enrollment.passI)/count(enrollment) <+ 0
	}
	
	relation Enrollment {
		Student *
		Course  *
		
		grade      : Float?
		pass       : Boolean = grade >= 5.5 <+ false
		passI      : Int = pass?1:0
	}
	
data

  Student alice   { name="Alice" }
  Student bob     { name="Bob" }
  Student charlie { name="Charlie" }

  Course  math { name="Mathematics" }

  Enrollment { student:alice   course:math grade=9.0 }
  Enrollment { student:bob     course:math grade=5.0 }
  Enrollment { student:charlie course:math }
  
execute

	"Did alice pass math?"
	alice.enrollment.pass
  
  ""
  "The average grade at Mathematics:"
  math.averageGrade
  
  ""
  "The pass percentage at Mathematics:"
  math.passPerc
  
	""
	"Bob's classmates (including self):"
	bob.course.student.name
