module weblab

model

  entity Student {
    name : String  
  }

  entity Course {
    avgGrade : Int? = this.assignment.avgGrade
  }

  relation Enrollment {
    Course * 
    Student *
    
    pass  : Boolean = this.studentAssignment.pass
    grade : Int ?   = this.studentAssignment.grade
  }

  relation CourseAssignments {
    Course 1 
    Assignment ?
  }

  relation Submission {
    Student * 
    Assignment * 

    grade  : Int  
    passed : Boolean  = grade >= 6
  }

  entity Assignment {
    avgGrade            : Int ? = avgGradeSubmissions <+ avgGradeParts
    
    avgGradeSubmissions : Int ? = avg ( this.submission.grade )
    avgGradeParts       : Int ? = avg ( this.part.avgGrade )
  }
  
  relation AssignmentPart {
    Assignment * whole <- wholeAssignment
    Assignment ? part  <- partAssignment
  }
  
  entity StudentAssignment {
  	pass      : Boolean = passGrade && passParts
  	
    passGrade : Boolean = grade >= 6 <+ false
    passParts : Boolean = conj( this.part.pass ) <+ true 
  	
    grade           : Int? = gradeSubmission <+ gradeParts
    
    gradeSubmission : Int? = this.submission.grade
    gradeParts      : Int? = avg ( this.part.grade )
  }
  
  relation StudentAssignmentPart {
    StudentAssignment * whole <- wholeAssignment
    StudentAssignment ? part  <- partAssignment
  }
  
  relation StudentAssignmentEnrollment {
    StudentAssignment ?
    Enrollment 1
  }

  relation StudentAssignmentSubmission {
    Submission 1
    StudentAssignment ?
  }

data 
  Student alice { name = "Alice" }
  Student bob   { name = "Bob" }
  Course compiler { }

  Enrollment enA { student : alice course : compiler }
  Enrollment enB { student : bob   course : compiler }

  Assignment compAssignments { }  CourseAssignments  { course : compiler assignment : compAssignments }

  Assignment exam { }
  Assignment practical { }
  Assignment practicalA { }
  Assignment practicalB { }

  AssignmentPart { whole : compAssignments part : exam         }
  AssignmentPart { whole : compAssignments part : practical    }
  AssignmentPart { whole : practical       part : practicalA   }
  AssignmentPart { whole : practical       part : practicalB   }

  Submission s1 { student : alice assignment : practicalA grade = 6  }
  Submission s2 { student : alice assignment : practicalB grade = 9  }
  Submission s3 { student : alice assignment : exam       grade = 6  }
  Submission s4 { student : bob   assignment : exam       grade = 10 }
  
  StudentAssignment compAlice { }
  StudentAssignment examAlice { }
  StudentAssignment practicalAlice { }
  StudentAssignment practicalAAlice { }
  StudentAssignment practicalBAlice { }
  StudentAssignment compBob { }
  StudentAssignment examBob { }
  StudentAssignment practicalBob { }
  StudentAssignment practicalABob { }
  StudentAssignment practicalBBob { }
  
  StudentAssignmentPart { whole : compAlice      part : examAlice       }
  StudentAssignmentPart { whole : compAlice      part : practicalAlice  }
  StudentAssignmentPart { whole : practicalAlice part : practicalAAlice }
  StudentAssignmentPart { whole : practicalAlice part : practicalBAlice }
  StudentAssignmentPart { whole : compBob        part : examBob         }
  StudentAssignmentPart { whole : compBob        part : practicalBob    }
  StudentAssignmentPart { whole : practicalBob   part : practicalABob   }
  StudentAssignmentPart { whole : practicalBob   part : practicalBBob   }

  StudentAssignmentEnrollment { studentAssignment : compAlice enrollment : enA }
  StudentAssignmentEnrollment { studentAssignment : compBob   enrollment : enB }

  StudentAssignmentSubmission { studentAssignment : practicalAAlice submission : s1 }
  StudentAssignmentSubmission { studentAssignment : practicalBAlice submission : s2 }  
  StudentAssignmentSubmission { studentAssignment : examAlice       submission : s3 }
  StudentAssignmentSubmission { studentAssignment : examBob         submission : s4 }

execute

  "Alice's compiler pass & grade:"
  enA.pass
  enA.grade

	""
	"Bob's compiler pass & grade:"
	enB.pass
  enB.grade

	""
  "The average grade of all submissions:"
  compiler.avgGrade
  