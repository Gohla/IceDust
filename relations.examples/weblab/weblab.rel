module weblab

// TODO: add charlie, practical sub asssignments, grade weights, and deadlines

model

  entity Student {
    name       : String
  }
  
  entity Course {
    name       : String
    avgGrade   : Float?  = assignment.avgGrade
    passPerc   : Float?  = assignment.passPerc
  }
  
  entity Enrollment {
    name       : String = course.name + " " +student.name
  }
  
  relation Course.enrollments *  <-> 1 Enrollment.course
  relation Student.enrollments * <-> 1 Enrollment.student
  
  entity Assignment {
    name       : String
    question   : String?
    minimum    : Float?
    avgGrade   : Float?  = avg(submissions.grade)
    passPerc   : Float?  = sum(submissions.passInt) / count(submissions) * 100.0
  }
  
  entity Submission {
    name       : String  = assignment.name + " " + student.name
    answer     : String?
    
    grade      : Float?  = if(childPass) childGrade else no value (default)
    pass       : Boolean = grade >= (assignment.minimum<+0.0) && childPass <+ false
    childGrade : Float?  = avg(children.grade)
    childPass  : Boolean = conj(children.pass)
  
    passInt    : Int     = if(pass) 1 else 0
    best       : Boolean = grade == max(assignment.submissions.grade) <+ false
  }
  
  relation Assignment.parent     ? <-> * Assignment.children
  relation Submission.parent     ? <-> * Submission.children
  relation Submission.student    1 <-> * Student.submissions
  relation Submission.assignment 1 <-> * Assignment.submissions
  
  relation Course.assignment     1 <-> ? Assignment.course
  relation Enrollment.submission 1 <-> ? Submission.enrollment

data

  alice : Student {
    name = "Alice"
  }
  bob : Student {
    name = "Bob"
  }
  charlie : Student {
    name = "Charlie"
  }
  math : Course {
    name = "Math"
    assignment = 
      mathAssignment {
        name = "Math Assignments"
        minimum = 6.0
        children =
          exam {
            name = "Exam"
            question = "1+1=?"
            minimum = 5.0
          },
          practical {
            name = "Practical"
            question = "1/0=?"
            minimum = 5.0
          }
      }
    enrollments = 
      enA {
        student = alice
        submission =
          mathAlice {
            assignment = mathAssignment
            children = 
              examAlice {
                assignment = exam
                answer = "Good"
                grade = 7.0
                student = alice
              },
              practicalAlice {
                assignment = practical
                answer = "Great"
                grade = 8.0
                student = alice
              }
            student = alice
          }
      },
      enB {
        student = bob
        submission =
          mathBob {
            assignment = mathAssignment
            children = 
              examBob {
                assignment = exam
                answer = "Bad"
                grade = 3.0
                student = bob
              },
              practicalBob {
                assignment = practical
                answer = "Perfect"
                grade = 10.0
                student = bob
              }
            student = bob
          }
      }
  }
  
execute

  "The average grade at Mathematics:"
  math.avgGrade
  ""
  "The pass percentage at Mathematics:"
  math.passPerc
