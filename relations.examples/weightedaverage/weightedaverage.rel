module weightedaverage

model

  entity AvgNode {
    weight : Int ?
    value  : Int = valueWeightedChildren /. weightChildren <+ 0
    
    valueWeighted : Int = (weight<+0) * value
    
    valueWeightedChildren : Int = sum( children.valueWeighted ++ leafChildren.valueWeighted )
    weightChildren        : Int = sum( children.weight        ++ leafChildren.weight        )
  }
  
  entity ValNode {
    weight : Int
    value  : Int
    
    valueWeighted : Int = weight * value
  }

  relation AvgNode.parent ? <-> * AvgNode.children {}

  relation ValNode.leafParent 1 <-> * AvgNode.leafChildren {}

data

  a3 : AvgNode {
    children = 
      a1 : AvgNode {
        weight = 10
        leafChildren =
          v1 : ValNode {value=100 weight=2},
          v2 : ValNode {value=500 weight=3}
      },
      a2 : AvgNode {
        weight = 15
        leafChildren =
          v3 : ValNode {value=228 weight=1},
          v4 : ValNode {value=332 weight=1},
          v5 : ValNode {value=376 weight=1}
      }
      
  }

execute

  a3.value