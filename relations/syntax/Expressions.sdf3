module Expressions

imports
	Common
	Data
	Model

templates // brackets

  Exp                    = <(<Exp>)>               {bracket}

templates // operators

  Exp.MemberAccess       = <<Exp> . <ID>>          {left}
  Exp.Not                = <!<Exp>>                {right}

  Exp.Multiplication     = <<Exp> * <Exp>>         {left}
  Exp.Division           = <<Exp> / <Exp>>         {left}
  Exp.Modulo             = <<Exp> % <Exp>>         {left}
  Exp.Addition           = <<Exp> + <Exp>>         {left}
  Exp.Subtraction        = <<Exp> - <Exp>>         {left}

  Exp.LessThan           = <<Exp> \< <Exp>>        {left}
  Exp.LessThanEqual      = <<Exp> \<= <Exp>>       {left}
  Exp.GreaterThan        = <<Exp> \> <Exp>>        {left}
  Exp.GreaterThanEqual   = <<Exp> \>= <Exp>>       {left}
  Exp.Equal              = <<Exp> == <Exp>>        {left}
  Exp.Inequal            = <<Exp> != <Exp>>        {left}
  Exp.And                = <<Exp> && <Exp>>        {left}
  Exp.Or                 = <<Exp> || <Exp>>        {left}
  Exp.TernaryConditional = <<Exp> ? <Exp> : <Exp>> {right}

  Exp.Merge              = <<Exp> ++ <Exp>>        {left}
  Exp.ChoiceLeft         = <<Exp> \<+ <Exp>>       {left}

templates // built-in functions

  Exp.Min                = <min ( <Exp> )>
  Exp.Max                = <max ( <Exp> )>
  Exp.Avg                = <avg ( <Exp> )>
  Exp.Sum                = <sum ( <Exp> )>
  Exp.Concat             = <concat ( <Exp> )>
  Exp.Count              = <count ( <Exp> )>
  Exp.Conj               = <conj ( <Exp> )>
  Exp.Disj               = <disj ( <Exp> )>

templates // references

  Exp.This               = <this>
  Exp.Ref                = <<ID>>
  
templates // literal values

  Exp                    = <<LitVal>>
  LitVal.String          = <"<STRING>">
  LitVal.Int             = <<INT>>
  LitVal.True            = <true>
  LitVal.False           = <false>


template options
context-free priorities
	{ left:
		Exp.MemberAccess
	} >	
	{ right:
		Exp.Not
	} >
	{ left:
		Exp.Multiplication
		Exp.Division
		Exp.Modulo
	} >
	{ left:
		Exp.Addition
		Exp.Subtraction
	} >
	{ left:
		Exp.LessThan
		Exp.LessThanEqual
		Exp.GreaterThan
		Exp.GreaterThanEqual
	} >
	{ left:
		Exp.Equal
		Exp.Inequal
	} >
	{ left:
		Exp.And
	} >
	{ left:
		Exp.Or
	} >
	{ right:
		Exp.TernaryConditional
	} >
	{ left:
		Exp.Merge
	} >
	{ left:
		Exp.ChoiceLeft
	}