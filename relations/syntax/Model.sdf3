module Model

imports
	Common
	Expressions
	Types

templates // both entities and relations are EntityTypeDefs

	Model.EntityTypeDef = <
		entity<Placeholder1> <ID> {
			<RoleOrAttribute*;separator="\n">
		}
	>
	
	Placeholder1.EntityType = <>
	
	Model.EntityTypeDef = <
		relation<Placeholder2> <ID> {
			<RoleOrAttribute*;separator="\n">
		}
	>
	
	Placeholder2.RelationType = <>

templates // roles and attributes

	RoleOrAttribute								= <<Role>>
	RoleOrAttribute								= <<Attribute>>
	RoleOrAttribute								= <<Shortcut>>

templates

	Attribute.Attribute 					= <<ID> : <PrimitiveType> <Multiplicity?> <Derivation?>>
	
	Derivation.Derivation 				= <= <Exp> <DerivationType>>
	
	DerivationType.DefaultValue		= <(default)>
	DerivationType.Derivation			= <(derivation)>
	DerivationType.Derivation			= <>
	
	Shortcut.Shortcut							= <<RoleRef>.<ID> <ID?> \<-\> <ID?> <RoleRef>.<ID>>

templates

	Role.Role											= <<ID> <Multiplicity> <ID?> <Inverse?>>
	Inverse												= <\<- <ID>>
	
	EntityType.EntityType 				= <<ID>>
	RoleRef.RoleRef							= <<ID>>  
	
	Multiplicity.One	 						= <1>
	Multiplicity.ZeroOrOne				= <?>
	Multiplicity.OneOrMore				= <+>
	Multiplicity.ZeroOrMore				= <*>

