module Model

imports
	Common
	Expressions
	Types

templates // both entities and relations are Entitys

	Model.Entity = <
		entity<Placeholder1> <ID> {
			<RoleOrAttribute*;separator="\n">
		}
	>
	
	Placeholder1.EntityRef = <>
	
	Model.Entity = <
		relation<Placeholder2> <ID> {
			<RoleOrAttribute*;separator="\n">
		}
	>
	
	Placeholder2.RelationType = <>

templates // roles and attributes

	RoleOrAttribute								= <<Role>>
	RoleOrAttribute								= <<Attribute>>
	RoleOrAttribute								= <<Shortcut>>

templates

	Attribute.Attribute 					= <<ID> : <PrimitiveType> <Multiplicity?> <Derivation?>>
	
	Derivation.Derivation 				= <= <Exp> <DerivationType>>
	
	DerivationType.DefaultValue		= <(default)>
	DerivationType.Derivation			= <(derivation)>
	DerivationType.Derivation			= <>
	
	Shortcut.Shortcut							= <<RoleRef>.<ID> <ID?> \<-\> <ID?> <RoleRef>.<ID>>

templates

	Role.Role											= <<ID> <Multiplicity> <ID?> <Inverse?>>
	Inverse												= <\<- <ID>>
	
	EntityRef.EntityRef 				= <<ID>>
	RoleRef.RoleRef							= <<ID>>  
	
	Multiplicity.One	 						= <1>
	Multiplicity.ZeroOrOne				= <?>
	Multiplicity.OneOrMore				= <+>
	Multiplicity.ZeroOrMore				= <*>

