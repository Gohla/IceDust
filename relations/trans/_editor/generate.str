module _editor/generate

imports // constructors

  signatures/Modules-sig
  api/_runtime-constructors
  
imports // functions

  _editor/analysis
  api/_generated
  api/_runtime
  api/module-api
  api/module-names-api
  desugaring/desugar
  generating/_ir/paths
  generating/java/generate
  generating/webdsl/generate
  generating/webdsl/java/generate
  generating/webdsl-eventual-no-ui/generate
  lib/files
  lib/call-external
  prettyprinting/pp

signature sorts
  
  TargetLanguage
  
signature constructors
  
  Java               : TargetLanguage
  WebDSL             : TargetLanguage
  WebDSLEager        : TargetLanguage
  WebDSLEventual     : TargetLanguage
  WebDSLEventualNoUI : TargetLanguage
  
  ImplStrategy : CompilerOptions
  
  CalcOnRead           : ImplStrategy
  CalcOnWrite          : ImplStrategy
  EventuallyConsistent : ImplStrategy

rules // Builders
  
  generate-java                       = build(error-handler-editor|Java(),CalcOnRead(),1)
  generate-java-test                  = build(error-handler-tests |Java(),CalcOnRead(),1)
  compile-java                        = build(error-handler-editor|Java(),CalcOnRead(),2)
  compile-java-test                   = build(error-handler-tests |Java(),CalcOnRead(),2)
  execute-java                        = build(error-handler-editor|Java(),CalcOnRead(),3);debug
  execute-java-test                   = build(error-handler-tests |Java(),CalcOnRead(),3)
  generate-java-incr                  = build(error-handler-editor|Java(),CalcOnWrite(),1)
  generate-java-incr-test             = build(error-handler-tests |Java(),CalcOnWrite(),1)
  compile-java-incr                   = build(error-handler-editor|Java(),CalcOnWrite(),2)
  compile-java-incr-test              = build(error-handler-tests |Java(),CalcOnWrite(),2)
  execute-java-incr                   = build(error-handler-editor|Java(),CalcOnWrite(),3);debug
  execute-java-incr-test              = build(error-handler-tests |Java(),CalcOnWrite(),3)

  generate-webdsl                     = build(error-handler-editor|WebDSL(),CalcOnRead(),1)
  generate-webdsl-test                = build(error-handler-tests |WebDSL(),CalcOnRead(),1)
  generate-webdsl-eager               = build(error-handler-editor|WebDSL(),CalcOnWrite(),1)
  generate-webdsl-eager-test          = build(error-handler-tests |WebDSL(),CalcOnWrite(),1)
  generate-webdsl-eventual            = build(error-handler-editor|WebDSL(),EventuallyConsistent(),1)
  generate-webdsl-eventual-test       = build(error-handler-tests |WebDSL(),EventuallyConsistent(),1)
  generate-webdsl-eventual-no-ui      = build(error-handler-editor|WebDSL(),EventuallyConsistent(),1)
  generate-webdsl-eventual-no-ui-test = build(error-handler-tests |WebDSL(),EventuallyConsistent(),1)

rules // Error handlers (editor: report error, do not fail, tests: fail with error)

  error-handler-editor : msg -> ("ERROR_PLACEHOLDER", msg)
    with
      err-msg(|msg)

  error-handler-tests : msg -> None()
    with
      with(fail|msg)

rules // Build in steps

  build(error-handler|lang, implstr, phase): (selected, position, ast, path, project-path-wrong) -> result
    with
      moduleName  := <module-get-modulename>selected;
      result      := <build(error-handler|lang, implstr, phase, moduleName, path)>

  build(error-handler|lang, implstr, phase, moduleName, relativePath): _ -> result
    with
      fullPath    := <full-path(|relativePath)>;
      eclipsePath := <eclipse-path>relativePath
    with
      clear-index2;
      <index-store-prop2(|ImplStrategy(), moduleName)>implstr;
      error* := <_task-get-messages(|eclipsePath)>
    with
      if not([] := error*) then
        <error-handler>"Fix errors before generating code. Exiting code generation.";
        result := None()
      else
        // phase 1: generate files
        switch !lang
          case ?Java():
            (targetCode, error') := <generate-java-code(error-handler)>moduleName;
            targetFileName := <java-generated-path(|relativePath)>;
            java-library-paths(|fullPath);copy-dir
          case ?WebDSL():
            (targetCode, error') := <generate-webdsl-code(error-handler)>moduleName;
            targetFileName := <webdsl-generated-path(|relativePath)>;
            webdsl-library-paths(|fullPath);copy-dir;
            webdsl-java-library-paths(|fullPath);copy-dir;
            !(<webdsl-saved-but-not-build(|fullPath)>,"");write-file;
            if <modulename-is-eventuallyconsistent>moduleName then
              (targetCode2, error2') := <generate-webdsl-java-code(error-handler)>moduleName;
              targetFileName2 := <webdsl-java-generated-path(|fullPath)>;
              <write-file>(<webdsl-java-generated-path(|fullPath)>,targetCode2)
            end
        end;
        if <eq>(1, phase) then
          result := (targetFileName, targetCode)
        else
          if not([] := error') then
            <error-handler>"Code generation failed.";
            result := None()
          else
            // phase 2: compile generated files
            <write-file> (targetFileName, targetCode);
            (exitCode, stdOut, stdErr) := <call-javac(|<java-generated-path(|fullPath)>)>;
            if not(0 := exitCode) then
              <error-handler>stdOut;
              <error-handler>stdErr;
              <error-handler>"Java compilation failed."
            end;
            if <eq>(2, phase) then
              result := None()
            else
              // phase 3: execute compiled code
              (exitCode', stdOut', stdErr') := <call-java(|<java-compiled-path(|fullPath)>)>;
              if not(0 := exitCode') then
                <error-handler>stdOut';
                <error-handler>stdErr';
                <error-handler>"Java execution failed.";
                result := None()
              else
                result := stdOut'
              end
            end 
          end 
        end 
      end
