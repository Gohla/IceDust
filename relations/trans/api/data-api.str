module api/data-api

imports // constructors

	signatures/Data-sig
	signatures/Modules-sig
	signatures/Model-sig
  desugaring/constructors

imports // functions
		
	lib/origin

rules // query
	
	datadef-get-data = ?DataSection(<id>)
	
  entityinstance-get-type = ?EntityInstance(_, EntityRef(<id>), _)
  entityinstance-get-type = ?EntityInstance(_, <id>, _) // no type in concrete syntax (type inferred)
	entityinstance-get-name = ?EntityInstance(<id>, _, _)
	entityinstance-get-rolevalues = ?EntityInstance(_, _, <id>)
	entityinstance-get-attrvalues = ?EntityInstance(_, _, <id>)
	entityinstance-get-membervalues = ?EntityInstance(_, _, <id>)
  
  membervalue-get-name = ?MemberValue(MemberRef(<id>), _)
  membervalue-get-name = ?MemberValue(NaBLHelp(_, <id>), _)
  membervalue-get-membername = ?MemberValue(MemberRef(<id>), _)
  membervalue-get-membername = ?MemberValue(NaBLHelp(MemberRef(<id>), _), _)
  membervalue-get-expr = ?MemberValue(_, <id>)

rules // properties
	
	is-datadef = ?DataSection(_)
	
	is-entityinstance = ?EntityInstance(_, _, _)
	
  is-membervalue = ?MemberValue(_,_)

	entityinstance-has-no-name = ?EntityInstance(None(), _, _)
	entityinstance-has-no-name = ?RelationInstance(None(), _, _)

rules // manipulate
	
	datadef-set-data(|op) = DataSection(!op)
	
  entityinstance-set-name(|name) = EntityInstance(!name, id, id)
  entityinstance-set-name(|name) = RelationInstance(!name, id, id)
	
	membervalue-set-member(member) = MemberValue(member, id)
	
	membervalue-dup-member   = membervalue-set-member(\r@MemberRef(m) -> NaBLHelp(r, m)\)
	membervalue-dedup-member = membervalue-set-member(\NaBLHelp(r, _) -> r\)
	
rules // representations    
  
  entityinstance-to-entityinstanceref: EntityInstance(ei, ty, members) -> EntityInstanceRef(ei)
  entityinstance-to-entityinstanceref: RelationInstance(ei, ty, members) -> EntityInstanceRef(ei)

rules

  membervalue-getall-einames = collect-all(eiwrefnames)

  eirefname: EntityInstanceRef(name) -> name
  eiwrefnames: EntityInstanceWrapper(EntityInstanceRef(relname), EntityInstanceRef(name)) -> (relname,name)
  eiwrefnames: EntityInstanceWrapper(None(), EntityInstanceRef(name))                                       -> (None(),name)

rules

  is-comma = ?Comma(_, _)
  