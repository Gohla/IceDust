module trans/api/rules-api

imports

	src-gen/signatures/Model-sig
	src-gen/signatures/Rules-sig
	src-gen/signatures/Expressions-sig
	trans/desugaring/constructors
		
	trans/lib/origin

signature constructors
	
	LHS: HS
	NHS: HS
	RHS: HS

rules // query (in all stages of desugaring)

	rule-get-params = ?Rule(_, <id>, _)
	rule-get-body   = ?Rule(_, _, <id>)
	
	param-get-name = ?Param(<id>, _, _)
	
	rulebody-get-lhs = ?RuleBody(<id>, _, _, _)
	rulebody-get-nhs = ?RuleBody(_, <id>, _, _)
	rulebody-get-rhs = ?RuleBody(_, _, <id>, _)
	
	get-hs = is-lhs;!LHS()
	get-hs = is-nhs;!NHS()
	get-hs = is-rhs;!RHS()

	node-to-varname = ?LHSNode(NaBLHelp(<id>, _), _, _)
	node-to-varname = ?NHSNode(NaBLHelp(<id>, _), _, _)
	node-to-varname = ?RHSNode(NaBLHelp(<id>, _), _, _)

	node-to-varname = ?LHSNode(<id>, _, _)
	node-to-varname = ?NHSNode(<id>, _, _)
	node-to-varname = ?RHSNode(<id>, _, _)
	
	node-to-varname: edge -> node2-var
		where
			(node1-var, edge-var, node2-var) := <edge-to-varnames>edge

	edge-get-varname = ?LHSEdge(LHSTsHelp(_, _, LHSEdgeName(NaBLHelp(<id>, _))), _)
	edge-get-varname = ?NHSEdge(NHSTsHelp(_, _, NHSEdgeName(NaBLHelp(<id>, _))), _)
	edge-get-varname = ?RHSEdge(RHSTsHelp(_, _, RHSEdgeName(NaBLHelp(<id>, _))), _)
	
	edge-get-varname = ?LHSEdge(LHSTsHelp(_, _, LHSEdgeName(<id>)), _)
	edge-get-varname = ?NHSEdge(NHSTsHelp(_, _, NHSEdgeName(<id>)), _)
	edge-get-varname = ?RHSEdge(RHSTsHelp(_, _, RHSEdgeName(<id>)), _)
	
	edge-get-varname = ?LHSEdge(LHSTsHelp(_, _, <id>), _)
	edge-get-varname = ?NHSEdge(NHSTsHelp(_, _, <id>), _)
	edge-get-varname = ?RHSEdge(RHSTsHelp(_, _, <id>), _)

	edge-to-varnames: LHSEdge(LHSTsHelp(node1, _, LHSEdgeName(NaBLHelp(edge-var, _))), node2) -> (<node-to-varname>node1,edge-var,<node-to-varname>node2)
	edge-to-varnames: NHSEdge(NHSTsHelp(node1, _, NHSEdgeName(NaBLHelp(edge-var, _))), node2) -> (<node-to-varname>node1,edge-var,<node-to-varname>node2)
	edge-to-varnames: RHSEdge(RHSTsHelp(node1, _, RHSEdgeName(NaBLHelp(edge-var, _))), node2) -> (<node-to-varname>node1,edge-var,<node-to-varname>node2)
	
	edge-to-varnames: LHSEdge(LHSTsHelp(node1, _, LHSEdgeName(edge-var)), node2) -> (<node-to-varname>node1,edge-var,<node-to-varname>node2)
	edge-to-varnames: NHSEdge(NHSTsHelp(node1, _, NHSEdgeName(edge-var)), node2) -> (<node-to-varname>node1,edge-var,<node-to-varname>node2)
	edge-to-varnames: RHSEdge(RHSTsHelp(node1, _, RHSEdgeName(edge-var)), node2) -> (<node-to-varname>node1,edge-var,<node-to-varname>node2)
	
	edge-to-varnames: LHSEdge(LHSTsHelp(node1, _, edge-var), node2) -> (<node-to-varname>node1,edge-var,<node-to-varname>node2)
	edge-to-varnames: NHSEdge(NHSTsHelp(node1, _, edge-var), node2) -> (<node-to-varname>node1,edge-var,<node-to-varname>node2)
	edge-to-varnames: RHSEdge(RHSTsHelp(node1, _, edge-var), node2) -> (<node-to-varname>node1,edge-var,<node-to-varname>node2)

	node-to-type = ?LHSNode(_, EntityRef(<id>), _)
	node-to-type = ?NHSNode(_, EntityRef(<id>), _)
	node-to-type = ?RHSNode(_, EntityRef(<id>), _)

	edge-to-edgemodel = ?LHSEdge(LHSTsHelp(_, <id>, _), _)
	edge-to-edgemodel = ?NHSEdge(NHSTsHelp(_, <id>, _), _)
	edge-to-edgemodel = ?RHSEdge(RHSTsHelp(_, <id>, _), _)
	
	edge-get-node1 = ?LHSEdge(LHSTsHelp(<id>, _, _), _)
	edge-get-node1 = ?NHSEdge(NHSTsHelp(<id>, _, _), _)
	edge-get-node1 = ?RHSEdge(RHSTsHelp(<id>, _, _), _)
	
	edge-get-node2 = ?LHSEdge(LHSTsHelp(_, _, _), <id>)
	edge-get-node2 = ?NHSEdge(NHSTsHelp(_, _, _), <id>)
	edge-get-node2 = ?RHSEdge(RHSTsHelp(_, _, _), <id>)
	
	attr-get-name = ?Attr(<id>, _)
	attr-get-expr = ?Attr(_, <id>)
	
	attr-get-expr-ref-name = attr-get-expr;?Ref(<id>)

rules // properties
	
	is-rule = ?Rule(_, _, _)
	
	param-has-no-mult = param-has-mult(|None())
	
	param-has-mult(|mult) = ?Param(_, _, mult) 
	
	is-subrule = ?SubRule(_,_)
	
	subrule-has-no-mult = ?SubRule(None(), _)
	
	is-node = ?LHSNode(_, _, _)
	is-node = ?NHSNode(_, _, _)
	is-node = ?RHSNode(_, _, _)
	
	is-lhs = ?LHSNode(_, _, _)
	is-nhs = ?NHSNode(_, _, _)
	is-rhs = ?RHSNode(_, _, _)
	
	node-has-varname = not(node-has-no-varname)
	
	node-has-no-varname = ?LHSNode(None(), _, _)
	node-has-no-varname = ?NHSNode(None(), _, _)
	node-has-no-varname = ?RHSNode(None(), _, _)
	
	node-has-type = not(node-has-no-type)
	
	node-has-no-type = ?LHSNode(_, None(), _)
	node-has-no-type = ?NHSNode(_, None(), _)
	node-has-no-type = ?RHSNode(_, None(), _)
	
	node-is-def = node-get-defuse;?Def()
	
	node-get-defuse = ?LHSNode(NaBLHelp(_, <id>), _, _)
	node-get-defuse = ?NHSNode(NaBLHelp(_, <id>), _, _)
	node-get-defuse = ?RHSNode(NaBLHelp(_, <id>), _, _)
	
	is-edge = ?LHSEdge(_,_)
	is-edge = ?NHSEdge(_,_)
	is-edge = ?RHSEdge(_,_)
	
	is-lhs = ?LHSEdge(_, _)
	is-nhs = ?NHSEdge(_, _)
	is-rhs = ?RHSEdge(_, _)
	
	edge-has-varname = not(edge-has-no-varname)
	
	edge-has-no-varname = ?LHSEdge(LHSTsHelp(_, _, None()), _)
	edge-has-no-varname = ?NHSEdge(NHSTsHelp(_, _, None()), _)
	edge-has-no-varname = ?RHSEdge(RHSTsHelp(_, _, None()), _)
	
	is-attr = ?Attr(_, _)
	
	attr-is-ref = attr-get-expr;?Ref(_)

rules // manipulation
	
	rule-set-body(|body') = rule-set-body(!body')
	
	rule-set-body(body') = Rule(id, id, body')
	
	param-set-mult(|mult) = Param(id, id, !mult)
	
	rulebody-set-lhs(lhs) = RuleBody(lhs, id, id, id)
	rulebody-set-nhs(nhs) = RuleBody(id, nhs, id, id)
	rulebody-set-rhs(rhs) = RuleBody(id, id, rhs, id)
	
	subrule-set-mult(|mult) = SubRule(!mult, id)

	node-set-varname(|node-var') = LHSNode(!node-var', id, id)
	node-set-varname(|node-var') = NHSNode(!node-var', id, id)
	node-set-varname(|node-var') = RHSNode(!node-var', id, id)
	
	node-set-type(|node-type') = LHSNode(id, !EntityRef(node-type');custom-origin(|node-type'), id)
	node-set-type(|node-type') = NHSNode(id, !EntityRef(node-type');custom-origin(|node-type'), id)
	node-set-type(|node-type') = RHSNode(id, !EntityRef(node-type');custom-origin(|node-type'), id)
	
	node-set-params(ps) = LHSNode(id, id, ps)
	node-set-params(ps) = NHSNode(id, id, ps)
	node-set-params(ps) = RHSNode(id, id, ps)
	
	node-set-defuse(|d) = LHSNode(keep-origin(NaBLHelp(id, !d)), id, id)
	node-set-defuse(|d) = NHSNode(keep-origin(NaBLHelp(id, !d)), id, id)
	node-set-defuse(|d) = RHSNode(keep-origin(NaBLHelp(id, !d)), id, id)

	edge-set-varname(|edge-var') = LHSEdge(keep-origin(LHSTsHelp(id, id, !LHSEdgeName(edge-var');custom-origin(|edge-var'))), id)
	edge-set-varname(|edge-var') = NHSEdge(keep-origin(NHSTsHelp(id, id, !NHSEdgeName(edge-var');custom-origin(|edge-var'))), id)
	edge-set-varname(|edge-var') = RHSEdge(keep-origin(RHSTsHelp(id, id, !RHSEdgeName(edge-var');custom-origin(|edge-var'))), id)
	
	edge-set-node1(|n) = LHSEdge(keep-origin(LHSTsHelp(!n, id, id)), id)
	edge-set-node1(|n) = NHSEdge(keep-origin(NHSTsHelp(!n, id, id)), id)
	edge-set-node1(|n) = RHSEdge(keep-origin(RHSTsHelp(!n, id, id)), id)
	
	edge-set-node2(|n) = LHSEdge(id, !n)
	edge-set-node2(|n) = NHSEdge(id, !n)
	edge-set-node2(|n) = RHSEdge(id, !n)
	
	attr-set-name(n) = Attr(n, id)
	attr-set-expr(|e) = Attr(id, !e)
	
	// defuse makes sure there is exactly 1 NaBLHelp constructor, and removes the Ref(_) around varname if it is Bind()
	attr-set-defuse(|d): Attr(a, v) -> Attr(NaBLHelp(attrref, <custom-origin(|attrref)>d), v')
		where
			if NaBLHelp(attrref, _) := a then id else attrref := a end;
			if <is-string>v then varname := v else Ref(varname) := v end
		with
			if Bind() := d then
				v' := varname
			else
				v' := <custom-origin(|varname)>Ref(varname)
			end

rules // creation
	
	create-node-lhs(|name) = create-node-lhs(|name,None(),None())
	create-node-lhs(|name,type,params) = !LHSNode(name, None(), None());custom-origin(|name)
