module trans/api/rules-api

imports

	src-gen/signatures/Model-sig
	src-gen/signatures/Rules-sig
	trans/desugaring/constructors
		
	trans/lib/origin

rules // query (in all stages of desugaring)

	rule-get-params = ?Rule(_, <id>, _)
	rule-get-body   = ?Rule(_, _, <id>)
	
	param-get-name = ?Param(<id>, _, _)
	
	rulebody-get-lhs = ?RuleBody(<id>, _, _, _)
	rulebody-get-nhs = ?RuleBody(_, <id>, _, _)
	rulebody-get-rhs = ?RuleBody(_, _, <id>, _)

	node-to-varname = ?LHSNode(NaBLHelp(<id>, _), _, _)
	node-to-varname = ?NHSNode(NaBLHelp(<id>, _), _, _)
	node-to-varname = ?RHSNode(NaBLHelp(<id>, _), _, _)

	node-to-varname = ?LHSNode(<id>, _, _)
	node-to-varname = ?NHSNode(<id>, _, _)
	node-to-varname = ?RHSNode(<id>, _, _)
	
	node-to-varname: edge -> node2-var
		where
			(node1-var, edge-var, node2-var) := <edge-to-varnames>edge
	
	edge-to-varnames: LHSEdge(LHSTsHelp(node1, edge-model, LHSEdgeName(NaBLHelp(edge-var, _))), node2) -> (<node-to-varname>node1,edge-var,<node-to-varname>node2)
	edge-to-varnames: NHSEdge(NHSTsHelp(node1, edge-model, NHSEdgeName(NaBLHelp(edge-var, _))), node2) -> (<node-to-varname>node1,edge-var,<node-to-varname>node2)
	edge-to-varnames: RHSEdge(RHSTsHelp(node1, edge-model, RHSEdgeName(NaBLHelp(edge-var, _))), node2) -> (<node-to-varname>node1,edge-var,<node-to-varname>node2)
	
	edge-to-varnames: LHSEdge(LHSTsHelp(node1, edge-model, LHSEdgeName(edge-var)), node2) -> (<node-to-varname>node1,edge-var,<node-to-varname>node2)
	edge-to-varnames: NHSEdge(NHSTsHelp(node1, edge-model, NHSEdgeName(edge-var)), node2) -> (<node-to-varname>node1,edge-var,<node-to-varname>node2)
	edge-to-varnames: RHSEdge(RHSTsHelp(node1, edge-model, RHSEdgeName(edge-var)), node2) -> (<node-to-varname>node1,edge-var,<node-to-varname>node2)
	
	edge-to-varnames: LHSEdge(LHSTsHelp(node1, edge-model, edge-var), node2) -> (<node-to-varname>node1,edge-var,<node-to-varname>node2)
	edge-to-varnames: NHSEdge(NHSTsHelp(node1, edge-model, edge-var), node2) -> (<node-to-varname>node1,edge-var,<node-to-varname>node2)
	edge-to-varnames: RHSEdge(RHSTsHelp(node1, edge-model, edge-var), node2) -> (<node-to-varname>node1,edge-var,<node-to-varname>node2)

	node-to-type = ?LHSNode(_, EntityRef(<id>), _)
	node-to-type = ?NHSNode(_, EntityRef(<id>), _)
	node-to-type = ?RHSNode(_, EntityRef(<id>), _)

	edge-to-edgemodel = ?LHSEdge(LHSTsHelp(_, <id>, _), _)
	edge-to-edgemodel = ?NHSEdge(NHSTsHelp(_, <id>, _), _)
	edge-to-edgemodel = ?RHSEdge(RHSTsHelp(_, <id>, _), _) 

rules // properties
	
	is-rule = ?Rule(_, _, _)
	
	param-has-no-mult = param-has-mult(|None())
	
	param-has-mult(|mult) = ?Param(_, _, mult) 
	
	subrule-has-no-mult = ?SubRule(None(), _)
	
	node-has-varname = not(node-has-no-varname)
	
	node-has-no-varname = ?LHSNode(None(), _, _)
	node-has-no-varname = ?NHSNode(None(), _, _)
	node-has-no-varname = ?RHSNode(None(), _, _)
	
	node-has-type = not(node-has-no-type)
	
	node-has-no-type = ?LHSNode(_, None(), _)
	node-has-no-type = ?NHSNode(_, None(), _)
	node-has-no-type = ?RHSNode(_, None(), _)
	
	edge-has-varname = not(edge-has-no-varname)
	
	edge-has-no-varname = ?LHSEdge(LHSTsHelp(node1, edge-model, None()), node2)
	edge-has-no-varname = ?NHSEdge(NHSTsHelp(node1, edge-model, None()), node2)
	edge-has-no-varname = ?RHSEdge(RHSTsHelp(node1, edge-model, None()), node2)

rules // manipulation
	
	rule-set-body(|body') = rule-set-body(!body')
	
	rule-set-body(body') = Rule(id, id, body')
	
	param-set-mult(|mult) = Param(id, id, !mult)
	
	rulebody-set-lhs(lhs) = RuleBody(lhs, id, id, id)
	rulebody-set-nhs(nhs) = RuleBody(id, nhs, id, id)
	rulebody-set-rhs(rhs) = RuleBody(id, id, rhs, id)
	
	subrule-set-mult(|mult) = SubRule(!mult, id)

	node-set-varname(|node-var') = LHSNode(!node-var', id, id)
	node-set-varname(|node-var') = NHSNode(!node-var', id, id)
	node-set-varname(|node-var') = RHSNode(!node-var', id, id)
	
	node-set-type(|node-type') = LHSNode(id, !EntityRef(node-type');custom-origin(|node-type'), id)
	node-set-type(|node-type') = NHSNode(id, !EntityRef(node-type');custom-origin(|node-type'), id)
	node-set-type(|node-type') = RHSNode(id, !EntityRef(node-type');custom-origin(|node-type'), id)

	edge-set-varname(|edge-var') = LHSEdge(keep-origin(LHSTsHelp(id, id, !LHSEdgeName(edge-var');custom-origin(|edge-var'))), id)
	edge-set-varname(|edge-var') = NHSEdge(keep-origin(NHSTsHelp(id, id, !NHSEdgeName(edge-var');custom-origin(|edge-var'))), id)
	edge-set-varname(|edge-var') = RHSEdge(keep-origin(RHSTsHelp(id, id, !RHSEdgeName(edge-var');custom-origin(|edge-var'))), id)

rules // creation
	
	create-node-lhs(|name) = create-node-lhs(|name,None(),None())
	create-node-lhs(|name,type,params) = !LHSNode(name, None(), None());custom-origin(|name)
