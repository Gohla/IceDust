module trans/checking/check

imports

	lib/editor-common.generated
	lib/nabl/-
	lib/properties/-
	lib/task/-
	lib/types/-

	include/Relations
	
	trans/desugaring/desugar
	trans/naming/-

rules
	
	constraint-warning:
		EntityTypeDef(_, x, _) -> (x, $[Entity type names must start with a capital])
		where
			not(<string-starts-with-capital> x)

	constraint-error: EntityTypeDef(EntityType(), _, attributesAndRoles) -> <map(no-role);concat> attributesAndRoles
	no-role: x@Role(_,_,_) -> [(x, $[An entity cannot have roles.])]
	no-role: Attribute(_,_,_,_) -> []

rules // Check derivation types of attributes
	
	// Task-engine version	
	nabl-constraint(|ctx):
		x@AttributeValue(Identifier(name), _) -> <fail>
		with
			prop-task := <prop-create-lookup(|ctx, NablProp_derivation-type())> name;
			match     := <derivation-type-match(|ctx, Derivation())> prop-task;
			<task-create-error-on-success(|ctx, match, "Derivations cannot be assigned custom values.")> x

rules // Only relations allowed in navigators
	
	nabl-constraint(|ctx):
		NavigateIn(_, NavOr(), _, EntityType(entityType)) -> <fail>
		with
			prop-task := <prop-create-lookup(|ctx, NablProp_is-relation())> entityType;
			<task-create-error-on-failure(|ctx, prop-task, $[Type error: [entityType] is an entity, a relation is expected.])> entityType


rules // check if entities have all the required attributes assigned

	nabl-constraint(|ctx):
		EntityOrRelation(EntityType(e-ty), e, _, as) -> <fail>
		with
			ty-def 		 := <nabl-create-collectdefs(|ctx)> e-ty;
			[ty-attrs] := <nabl-resolve-all-tasks(|ctx, NablNsAttribute(), [])> [ty-def];
			as-defs    := <map(?AttributeValue(Identifier(<nabl-create-collectdefs(|ctx)>), _))> as;	
			as-attrs   := <task-create-combine(|ctx)> as-defs ;
			diff       := <task-create-diff(|ctx)> (ty-attrs, as-attrs);
			diff'      := <nabl-create-propconstraint(|ctx, NablProp_derivation-type(), diff)> Normal();
			diff''     := <nabl-create-propconstraint(|ctx, NablProp_multiplicity(), diff')> One();
			<task-create-error-on-success(|ctx, diff'', ["Provide all required attributes: ", diff''])> e
			//TODO: provide pp-ed attribute names in error message.
