module trans/desugaring/desugar

imports
	include/Relations
	
rules


desugar-all = bottomup(try(desugar);try(flatten-list));generate-entity-names


/**
 *	Default desugarings
 */
// remove all Some() constructs
desugar: Some(a) -> a

// make the top level construct indicate that it is desugared (we dont want any further processing if its not desugared)
desugar: Module(moduleName, model, data, execute) -> ModuleDesugared(moduleName, model, data, execute)


/**
 *	Expressions
 */
// desugar: AttributeNameThis(a)		->	AttributeName(This(), a)


/**
 *	Nonspecified multiplicities are One
 */
desugar: Attribute(a,b,None(),c) -> Attribute(a,b,One(),c)


/**
 *	Generate omitted names
 */
desugar = generate-role-name

generate-role-name: Role(EntityType(e), multiplicity, None()) -> Role(EntityType(e), multiplicity, e)

// desugar = generate-entity-name

generate-entity-names: ModuleDesugared(a, b, None(), d) -> ModuleDesugared(a, b, None(), d)
generate-entity-names: ModuleDesugared(a, b, DataDef(c), d) -> ModuleDesugared(a, b, DataDef(<map-with-index(try(generate-entity-name))>c), d)
generate-entity-name: (index, EntityOrRelation(EntityType(e-ty), None(), r, a)) -> EntityOrRelation(EntityType(e-ty), e, r, a)
	with
		e := $[[e-ty][index]]
		// e := <newname>e-ty

/**
 *	Navigators
 */
desugar: NavigateInOut(prevNav, navType, inRole, relation, outRole) ->
	NavigateOut(
    NavigateIn(prevNav, NavOr(), inRole, relation)
  , NavOr(), relation, outRole
  )

	
signature constructors
	ModuleDesugared	: ID * ModelDef * DataDef * Executable -> ModuleDesugared
	