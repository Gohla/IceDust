module trans/desugaring/nablhelp

imports
	
	include/Relations
	trans/desugaring/desugar
	
rules
	
	nablhelp =
		nablhelp1;
		nablhelp2
	
	nablhelp1 = try(sometd(entity-add-shortcut-types)) //requires the roles to be untouched
	
	nablhelp2 = try(sometd(role-duplicate-name))

rules // add types to shortcuts (so we can define in those types the shortcuts)
	
	entity-add-shortcut-types: Entity(a,entityName,members) -> Entity(a,entityName,members')
		with
			role-types := <filter(role-name-and-type)>members;
			members' := <map(try(add-shortcut-types(|role-types)))> members
	
	role-name-and-type: Role(type, _, name, _) -> (name, type)
	
	add-shortcut-types(|role-types): Shortcut(RoleRef(role1), b, RoleRef(role2), f) ->
																	 Shortcut(NaBLHelp(RoleRef(role1), type1), b, NaBLHelp(RoleRef(role2), type2), f)
		with
			type1 := <role-type>(role1, role-types);
			type2 := <role-type>(role2, role-types)
		
	role-type : (role, role-types) -> type
		with
			(_, type) := <filter(role-type(|role));first>role-types
		<+
			type := ""
	
	role-type(|role): (role2, type) -> <id>
		where
			<eq>(role, role2)

	first: [a] -> a
	first: [a|_] -> a

rules // duplicate role types (so one can define and one can refer)

	role-duplicate-name: Role(a, b, c, d) -> Role(NaBLHelp(a, a), b, c, d)

