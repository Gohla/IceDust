module trans/desugaring/omitted

imports
	
	lib/runtime/editor/origins
	trans/lib/origin
	trans/lib/new-name
	trans/lib/string
	trans/lib/traverse
	include/Relations
	src-gen/signatures/Rules-sig
	trans/desugaring/desugar

rules
	
	generate-omitted =
		bottomup(try(omitted1));
		bottomup(try(omitted2));
		bottomup(try(omitted3))
	
	omitted1 = attr-generate-mult
	omitted1 = role-generate-name
	omitted1 = entity-generate-inverse-names
	omitted1 = datadef-generate-entity-names
	omitted1 = param-generate-mult
	omitted1 = subrule-generate-mult
	omitted1 = node-move-name-to-type
	
	omitted2 = entity-generate-shortcuts        // requires role-generate-name
	omitted2 = rule-generate-node-names         // requires node-move-name-to-type
	
	omitted3 = rule-generate-param-node-matches // requires param-generate-mult, matches same as rule-generate-node-names

rules // generate omitted multiplicities

	attr-generate-mult: Attribute(a,b,None(),c) -> Attribute(a,b,One(),c)

rules // generate omitted role names

	role-generate-name: Role(e, multiplicity, None(), inverse) -> Role(e, multiplicity, <origin-track-forced(lcfirst)>e, inverse)

rules // generate omitted inverse names
	
	entity-generate-inverse-names: Entity(a,entityName,roles) -> Entity(a,entityName,roles')
		with
			roles' := <map(try(generate-inverse-name(|entityName)))>roles

	generate-inverse-name(|entityName): Role(a, b, c, None()) -> Role(a, b, c, <custom-origin(lcfirst|a)>entityName)

rules	// generate omitted entity names

	datadef-generate-entity-names: DataDef(c) -> DataDef(c'')
		with
			(c', _) := <map-fold(try(generate-entity-name))>(c, []);
			c'' := <custom-origin(|c)>c'

	generate-entity-name: (ei@EntityInstance(er@EntityRef(e-ty), None(), r, a), history) -> (<custom-origin(|ei)>EntityInstance(er, <custom-origin(|e-ty)>e, r, a), history')
		with
			(e, history') := <new-name>(<lcfirst>e-ty, history)
	
rules // generate omitted shortcuts

	entity-generate-shortcuts: Entity(a,entityName,members) -> Entity(a,entityName,members'')
		with
			roles 				:= <filter(role-name);origin-track-forced(string-sort)>members;
			shortcuts			:= <filter(shortcut-roles)>members;
			shortcuts-all := <join-half>roles;
			shortcuts-add := <diff>(shortcuts-all, shortcuts);
			members-new		:= <map(create-shortcut)>shortcuts-add;
			members'			:= <conc>(members,members-new);
			members''			:= <custom-origin(|members)>members'
			
	role-name: Role(_, _, name, _) -> name
	
	shortcut-roles: Shortcut(RoleRef(role1), _, RoleRef(role2), _) -> <string-sort;ListToTuple>[role1,role2]
	
	join-half: [] -> []
	
	join-half: [h|t] -> <conc>(r,r2)
		with
			r := <join-half> t;
			r2:= <map(pair-with(|h))>t
			
	pair-with(|h): t -> (h,t)

	create-shortcut: (role1,role2) -> Shortcut(rr1, role2, rr2, role1)
		with
			rr1 := <custom-origin(|role1)>RoleRef(role1);
			rr2 := <custom-origin(|role2)>RoleRef(role2)

rules // generate omitted multiplicities

	param-generate-mult: Param(a, b, None()) -> Param(a, b, One())

	subrule-generate-mult: SubRule(None(), a) -> SubRule(One(), a)
	
rules // move name to type if capitalized
	
	node-move-name-to-type: Node(name, None(), a) -> Node(None(), <custom-origin(|name)>EntityRef(name), a)
		where
			<first-is-upper>name

rules // generate omitted node names

	rule-generate-node-names: Rule(r, p, b) -> Rule(r, p, b'')
		with
			(b', _) := <topdown-fold(try(generate-node-name))>(b, []);
			b'' := <custom-origin(|b)>b'

	generate-node-name: (n@Node(None(), er@EntityRef(n-ty), v), history) -> (<custom-origin(|n)>Node(n-name, er, v), history')
		with
			(n-name, history') := <new-name>(<lcfirst>n-ty, history)
			
rules // generated omitted param node matches
	
	rule-generate-param-node-matches: Rule(r, p, b@RuleBody(lhs,rhs,sub)) -> Rule(r, p, b'')
		with
			p-match := <filter(rule-param-one-to-match)>p;
			b' := RuleBody(<conc>(p-match,lhs),rhs,sub);
			b'' := <custom-origin(|b)>b'
	
	rule-param-one-to-match: Param(name, EntityRef(_), One()) -> <custom-origin(|name)>Node(name, None(), None())
	
