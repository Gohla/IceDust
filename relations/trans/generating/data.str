module trans/generating/data

imports

	lib/java/Java
	include/Relations
	
	trans/generating/expressions

rules
	
  to-java(err): DataDef(defs) -> <map(to-java(err));concat> defs

	to-java(err): EntityOrRelation(
									EntityType(x_type)
								, x_name
								, roleValues
								, attributeValues
								)
  -> 
 	|[
    x_type x_name = new x_type();
    ~attributeValuesJava*
    ~roleValuesJava*
 	]|
	with
		attributeValuesJava* := <map(to-java(err|x_name))> attributeValues;
		roleValuesJava*			:= <map(to-java(err|x_name, x_type));concat> roleValues

  to-java(err|x_name) : AttributeValue(Identifier(attributeName), attributeValue) ->
		stm |[
			x_name.x_setter(~attributeValueJava);
		]|
	  with
	  	x_setter := $[set_[attributeName]];
	  	attributeValueJava := <exp-to-java(err)>attributeValue

  to-java(err|x_entityName, entityType) :	RoleValue(RoleName(x_roleName), Identifier(x_otherName)) ->
  	[
			stm |[ x_entityName.x_roleName = x_otherName; ]|,
	  	stm |[ x_otherName.x_roleNameOther.add(x_entityName);]|
	  ]
	with
		x_roleNameOther := $[[entityType]_[x_roleName]]
