module trans/generating/data

imports

	lib/java/Java
	include/Relations
	
	trans/generating/expressions
	trans/naming/names
	trans/naming/_notNaBL

rules
	
  to-java(err): DataDef(defs) -> <map(to-java(err));concat> defs

	to-java(err): EntityOrRelation(
									EntityType(x_type)
								, x_name
								, roleValues
								, attributeValues
								)
  -> 
 	|[
    x_type x_name = new x_type();
    ~attributeValuesJava*
    ~roleValuesJava*
 	]|
	with
		attributeValuesJava* := <map(to-java(err|x_name))> attributeValues;
		roleValuesJava*			:= <map(to-java(err|x_name, x_type));concat> roleValues

  to-java(err|x_name) : AttributeValue(Identifier(attributeName), attributeValue) ->
		stm |[
			x_name.x_setter(~attributeValueJava);
		]|
	  with
	  	x_setter := $[set_[attributeName]];
	  	attributeValueJava := <exp-to-java(err)>attributeValue

  to-java(err|x_entityName, entityType) :	RoleValue(RoleName(x_roleName), Identifier(x_otherName)) -> [result1, result2]
		with
			x_roleNameOther := $[[entityType]_[x_roleName]];
			multiplicity := <get-multiplicity>x_roleName
		with
			result1 := stm |[ x_entityName.x_roleName = x_otherName; ]|;
			if <upper-many>multiplicity then
				result2 := stm |[ x_otherName.x_roleNameOther.add(x_entityName);]|
			else
				result2 := stm |[ x_otherName.x_roleNameOther = x_entityName;]|
			end
			  
