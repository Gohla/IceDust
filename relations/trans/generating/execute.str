module trans/generating/execute

imports

	lib/java/Java
	include/Relations

rules

  to-java(error-handler): Executable(executes) -> <map(to-java(error-handler);syso)>executes
  
  to-java(error-handler): AttributeName(Identifier(entityName), attributeName) ->
		Invoke(
       Method(MethodName(AmbName(Id(entityName)), Id($[get_[attributeName]])))
     , []
     )

	to-java(error-handler): NavigateIn(prevNav, _, RoleName(roleInName), EntityType(relationName)) ->
		Invoke(
      Method(MethodName(AmbName(Id(relationName)), Id($[[roleInName]_in])))
    , [<to-java(error-handler)>prevNav]
    )
  
  to-java(error-handler): NavigateOut(prevNav, _, EntityType(relationName), RoleName(roleOutName)) ->
		Invoke(
		  Method(MethodName(AmbName(Id(relationName)), Id($[[roleOutName]_out])))
		, [ <to-java(error-handler)>prevNav ]
		)
		
	//TODO: this rule is too general
	to-java(error-handler): Identifier(entityName) ->
		Invoke(
		   Method(MethodName(Id("toCollection")))
		 , [ExprName(Id(entityName))]
		 )
		 
	syso: javaExpr ->
		ExprStm(
		  Invoke(
		    Method(
		      MethodName(
		        AmbName(AmbName(Id("System")), Id("out"))
		      , Id("println")
		      )
		    )
		  , [ javaExpr ]
		  )
		)