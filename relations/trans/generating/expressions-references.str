module trans/generating/expressions-references

imports
	
	lib/nabl/interface
	lib/nabl/uri
	lib/types/query

	include/Relations
	lib/java/Java
	
	trans/naming/names
	trans/generating/expressions
	trans/generating/types

rules // attributes of expressions

	// This() has no type, so cannot use the static getter
	exp-to-java-stm(err|i): MemberAccess(This(), name) -> ([], result)
		with
			x_getter := $[get_[name]]
		with
			result :=	|[this.x_getter()]|
			

	exp-to-java-stm(err|i): e@MemberAccess(e1, name) -> (stms, exp)
		with
			x_expType := <get-type>e1;
			x_getter := $[get_[name]];
			(stms*, e1exp) := <exp-to-java-stm(err|i)>e1;
			refty := <java-type>e;
			j := <add>(i, <length>stms*);
			x_var := $[var[<inc>j]]
		with
			stms := bstm* |[
				~stms*
				refty x_var = x_expType.x_getter(~e1exp);
			]|;
			exp := expr |[x_var]|

rules // direct references

	exp-to-java-stm(err|i): Identifier(x_entityName) -> ([], expr |[x_entityName]|)
		where
			NablNsEntity() := <nabl-uri;nabl-uri-namespace>x_entityName
				 
	exp-to-java-stm(err|i): Identifier(name) -> <exp-to-java-stm(err|i)>MemberAccess(This(), name) //forward to this.name
		where
			NablNsAttribute() := <nabl-uri;nabl-uri-namespace>name
			
	exp-to-java-stm(err|i): This() -> ([], |[this]|)
