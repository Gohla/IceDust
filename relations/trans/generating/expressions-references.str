module trans/generating/expressions-references

imports
	
	lib/nabl/interface
	lib/nabl/uri
	lib/types/query

	include/Relations
	lib/java/Java
	
	trans/naming/names
	trans/generating/expressions

rules // attributes of expressions

	ref-to-java(err): AttributeName(exp, name) -> result
		where
			One()       := <get-multiplicity>exp <+
			ZeroOrOne() := <get-multiplicity>exp <+
			This()      := exp //an un-typed This(): unqualified attribute reference
		with
			expJava := <exp-to-java(err)>exp;
			x_getter := $[get_[name]]
		with
			result :=	|[~expJava.x_getter()]|

	ref-to-java(err): AttributeName(exp, name) -> result
		where
			ZeroOrMore():= <get-multiplicity>exp <+
			OneOrMore() := <get-multiplicity>exp
		with
			x_expType := <get-type>exp;
			x_getter := $[get_[name]];
			expJava := <exp-to-java(err)>exp
		with
			result := |[x_expType.x_getter(~expJava)]|

rules // direct references

	ref-to-java(err): Identifier(x_entityName) -> expr |[x_entityName]|
		where
			NablNsEntity() := <nabl-uri;nabl-uri-namespace>x_entityName
				 
	ref-to-java(err): Identifier(name) -> result
		where
			NablNsAttribute() := <nabl-uri;nabl-uri-namespace>name
		with
			result := <ref-to-java(err)>AttributeName(This(), name) //forward to this.name
			
	ref-to-java(err): This() -> |[this]|
