module generating/java/module

imports // constructors

	java/Java
	signatures/Modules-sig	
	desugaring/constructors

rules

	to-java(err):
		ModuleDesugared(moduleName, model, data, execute) ->
			compilation-unit |[
				import lib.relations.*;
				import java.util.Collection;
				import java.util.Date;
        import javax.lang.model.type.NullType;
				
				~mainClass
				~modelJava*
			]|
		with
			mainClass := <main-class-to-java(err)>ModuleDesugared(moduleName, model, data, execute);
			if ModelDef(_) := model then
				modelJava* := <to-java(err)>model
			else
				modelJava* := []
			end

	main-class-to-java(err):
		ModuleDesugared(moduleName, model, data, execute) ->
			type-dec |[
				class ~className	
				{ 
					public static void main(String[] args)
					{
						~dataJava*
						~executeJava*
					}
				}
			]|
		with
			if DataDef(_) := data then
				dataJava* := <to-java(err)>data
			else
				dataJava* := []
			end;
			if Executable(_) := execute then
				executeJava* := <to-java(err)>execute
			else
				executeJava* := []
			end;
			className := Id(moduleName)
		