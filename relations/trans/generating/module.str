module trans/generating/module

imports // constructors

	lib/java/Java
	src-gen/signatures/modules-sig	
	trans/desugaring/constructors

rules

	to-java(err):
		ModuleDesugared(moduleName, model, r, data, execute) ->
			compilation-unit |[
				import lib.relations.*;
				import java.util.Collection;
				
				~mainClass
				~modelJava*
			]|
		with
			mainClass := <main-class-to-java(err)>ModuleDesugared(moduleName, model, r, data, execute);
			if ModelDef(_) := model then
				modelJava* := <to-java(err)>model
			else
				modelJava* := []
			end

	main-class-to-java(err):
		ModuleDesugared(moduleName, model, r, data, execute) ->
			type-dec |[
				class ~className	
				{ 
					public static void main(String[] args)
					{
						~dataJava*
						~executeJava*
					}
					~rulesJava*
				}
			]|
		with
			if DataDef(_) := data then
				dataJava* := <to-java(err)>data
			else
				dataJava* := []
			end;
			if RulesDef(_) := r then
				rulesJava* := <to-java(err)>r
			else
				rulesJava* := []
			end;
			if Executable(_) := execute then
				executeJava* := <to-java(err)>execute
			else
				executeJava* := []
			end;
			className := Id(moduleName)
		