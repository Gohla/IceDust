module trans/generating/webdsl-eager/java/model

imports // constructors

	trans/api/constructors
	lib/java/Java

imports // functions

	trans/api/model-names-api

rules // attribute -> java compilation-unit
	
  to-webdsl-eager-java(err): x_name -> compilation-unit |[
		package derivations.generated;
		
		import java.util.Set;
		
		import webdsl.generated.domain.x_entity;
		import derivations.*;
		
		public class x_entityAttribute extends Derivation<x_entity> {
			
			public Set<x_entity> dirtyValues(){
				return x_entity.x_dirty();
			}
			
			public void cleanValues(){
				x_entity.x_clean();
			}
			
			public void updateValues(Set<x_entity> values){
				x_entity.x_update(values);
			}
		}
	]|
		with
			x_entity          := <attributename-get-entityname>x_name;
			x_entityAttribute := $[[x_entity]_[x_name]];
			x_dirty           := $[$static$[x_name]_dirty_];
			x_clean           := $[$static$[x_name]_dirty_clean_];
			x_update          := $[$static$[x_name]Set_[x_entity]__]
