module trans/generating/webdsl/expressions

imports // constructors

  lib/webdsl/constructors
  lib/webdsl/WebDSL
  src-gen/signatures/Data-sig
  src-gen/signatures/Expressions-sig  
  trans/desugaring/constructors

imports // functions

  trans/api/_runtime  
  trans/api/module-names-api
  trans/generating/_ir/expressions
  trans/generating/webdsl/model
  trans/generating/webdsl/data
  trans/generating/webdsl/types
  trans/naming/names

rules // WebDSL specific: no generics, explicit casts
  
  webdsl-lib-call-cast = ?TernaryConditional(_,_,_)

  webdsl-lib-call-cast = ?Merge(_, _)
  webdsl-lib-call-cast = ?ChoiceLeft(_, _)

rules // lib expression calls
  
  to-webdsl(err): e -> Call(Var("Expressions"), x_methodpostfix, e1*) // |[ Expressions.x_methodpostfix(e1)]| //TODO: fix webdsl concrete syntax for expression list
    where
      lib-call
    where
      not(webdsl-lib-call-cast)
    with
      arg*            := <get-arguments>e;
      e1*             := <map(to-webdsl(err))>arg*;
      x_methodpostfix := <lib-method-name>e
      
  to-webdsl(err): e -> Cast(Call(Var("Expressions"), x_methodpostfix, e1*), srt1) //|[ Expressions.x_methodpostfix(e1) as srt1 ]| //TODO: fix webdsl concrete syntax for expression list
    where
      lib-call
    where
      webdsl-lib-call-cast
    with
      arg*            := <get-arguments>e;
      e1*             := <map(to-webdsl(err))>arg*;
      x_methodpostfix := <lib-method-name>e;
      srt1            := <webdsl-type(err)>e
  
rules // literals
  
  to-webdsl(err): String(str1)                    -> String(str1)
  to-webdsl(err): Int(i_1)                        -> webdsl:e |[ i_1 ]|
  to-webdsl(err): Float(f_1)                      -> Float(f_1)//webdsl:e |[ f_1 ]|
  to-webdsl(err): True()                          -> webdsl:e |[ true ]|
  to-webdsl(err): False()                         -> webdsl:e |[ false ]|
  to-webdsl(err): Datetime(str1)                  -> webdsl:e |[Expressions.parseDatetime(e1)]| with e1 := String(str1)
  to-webdsl(err): NoValue()                       -> webdsl:e |[ null ]|

rules // casts
  
  to-webdsl(err): Cast(NoValue() , ex1-ty) -> |[ null as x ]| where x := <type-as-string>ex1-ty
  
  to-webdsl(err): Cast(ex1, ex1-ty) -> |[ Expressions.x_method(e1) ]|
    with
      e1 := <to-webdsl(err)>ex1;
      x_method := $[as[<postfix-id>ex1-ty]]

rules
  