module trans/generating/webdsl/model

imports // constructors

	trans/api/constructors
	lib/webdsl/WebDSL
	src-gen/signatures/model-sig
	src-gen/signatures/modules-sig
	trans/desugaring/constructors

imports // functions

	trans/api/_runtime
	trans/api/model-api
	trans/api/model-names-api
	trans/lib/search
	trans/naming/names
	trans/naming/_notNabl

rules // entities
	
  to-webdsl(err|ast): x_name -> 
		webdsl |[
			entity x_name {
				ebd1*
				ebd2*
				ebd3*
			}
		]|
	where
		is-entityname
	with
		ebd1* := <entityname-get-attributenames;map(to-webdsl(err|ast))>x_name;
		ebd2* := <entityname-get-rolenames;     map(to-webdsl(err))>x_name;
		ebd3* := <entityname-get-inversenames;  map(to-webdsl(err))>x_name

rules // attributes

	to-webdsl(err|ast): x_name -> webdsl |[ x_name : srt1 (default=null) ]|
		where
			is-attributename
		where
			attributename-is-normal
		with
			srt1 := <to-webdsl(err)><_get-type>x_name
			
	to-webdsl(err|ast): x_name -> webdsl |[ x_name : srt1 := e1 ]|
		where
			is-attributename
		where
			attributename-is-derivation
		with
			srt1 := <to-webdsl(err)><_get-type>x_name;
			e1   := <name-to-ast(|ast);attr-get-derivation;to-webdsl(err)>x_name
			
	to-webdsl(err|ast): x_name -> webdsl |[ x_name : srt1 (default=e1) ]|
		where
			is-attributename
		where
			attributename-is-default
		with
			srt1 := <to-webdsl(err)><_get-type>x_name;
			e1   := <name-to-ast(|ast);attr-get-derivation;to-webdsl(err)>x_name

rules // roles

	to-webdsl(err): x_name -> webdsl |[ x_name : x_type ]|
		where
			is-rolename
		with
			x_type := <_get-type>x_name

rules // inverses
	
	to-webdsl(err): x_name -> webdsl |[ x_name : x_type (inverse=x_rolename) ]|
		where
			is-inversename
		where
			get-multiplicity;upper-one
		with
			x_type := <_get-type>x_name;
			x_rolename := <inversename-get-rolename>x_name
	
	to-webdsl(err): x_name -> webdsl |[ x_name : {x_type} (inverse=x_rolename) ]|
		where
			is-inversename
		where
			get-multiplicity;upper-many
		with
			x_type := <_get-type>x_name;
			x_rolename := <inversename-get-rolename>x_name

rules
	