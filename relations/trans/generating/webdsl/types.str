module trans/generating/webdsl/types

imports // constructors

	lib/webdsl/constructors
	lib/webdsl/WebDSL
	src-gen/signatures/Types-sig
	trans/desugaring/constructors

imports // functions

	trans/api/_runtime
	trans/naming/names
	trans/naming/_notNaBL
	
rules // primitives
	
	type-to-webdsl(err): Boolean() -> webdsl |[ Bool ]|		
	type-to-webdsl(err): Int()     -> webdsl |[ Int ]|		
	type-to-webdsl(err): String()  -> webdsl |[ String ]|		
	type-to-webdsl(err): Float()   -> webdsl |[ Float ]|		
	type-to-webdsl(err): Datetime()-> webdsl |[ DateTime ]|
	type-to-webdsl(err): name      -> name                 where is-string

rules // webdsl types
	
	webdsl-type(err): name -> <webdsl-type(err|ty, mu)>
		with
			ty := <_get-type>name;
			mu := <get-multiplicity>name
	
	webdsl-type(err|ty, mu) = where(!mu;upper-one) ;!ty;type-to-webdsl(err);?srt1;!webdsl |[ srt1   ]|
	webdsl-type(err|ty, mu) = where(!mu;upper-many);!ty;type-to-webdsl(err);?srt1;!webdsl |[ [srt1] ]|

rules

	type-as-string: String()	-> "String"
  type-as-string: Int()		  -> "Integer"
  type-as-string: Boolean() -> "Boolean"
  type-as-string: Float()   -> "Float"
  type-as-string: Datetime()-> "DateTime"
  type-as-string: _ -> <id>
  	where is-string

rules
