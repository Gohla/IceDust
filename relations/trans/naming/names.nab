module trans/naming/names

imports
	
	include/Relations
	trans/desugaring/desugar
	
namespaces
	
	Module
	EntityType
	Entity
	Attribute
	Role
	This

properties

	is-relation			of EntityType	: Term						//true iff relation, unset if entity
	derivation-type of Attribute	: DerivationType	//either is Derivation(), has DefaultValue() or is Normal()
	fulfill-role 		of EntityType : EntityType			//points to the Relation of which it is a role (the relation is EntityType)
	
binding rules

	// modules
	ModuleDesugared(m, _, _, _) : 
		defines Module m
		scopes EntityType
		scopes Entity


	// entity types
	EntityTypeDef(_, e, _) :
		defines EntityType e
		scopes Attribute
		scopes Role
		scopes This
		implicitly defines This "this" of type e
		
	EntityType(e) :
		refers to EntityType e
		
	
	// entities
	EntityOrRelation(EntityType(entityType), e, _, _) :
		defines Entity e of type entityType
		scopes This
		implicitly defines This "this" of type entityType
	
	EntityName(e) :
		refers to Entity e
	
	
	// attributes
	Attribute(a, t, _) :
		defines Attribute a of type t
		
	AttributeName(This(), a) :
		refers to Attribute a in EntityType t
		where This "this" has type t
		
	AttributeName(EntityName(e), a) :
		refers to Attribute a in EntityType t
		where Entity e has type t

	
	// roles
	Role(EntityType(roleType), multiplicity, role) :
		defines Role role of type roleType //ignoring multiplicties in types atm
	
	RoleName(r):
		refers to Role r in EntityType t
		where This "this" has type t
		

	// navigators
	NavigateIn(_, _, inRole, EntityType(relationType)) :
		implicitly defines This "this" of type relationType in inRole
	NavigateOut(_, _, EntityType(relationType), outRole) :
		implicitly defines This "this" of type relationType in outRole
	
