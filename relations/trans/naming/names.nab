module trans/naming/names

imports

	include/Relations
	trans/desugaring/desugar
	trans/naming/_notNaBL
	
namespaces
	
	Module
	EntityType
	Entity
	Attribute
	Role
	This

properties

	is-relation					of EntityType	: Term						//true iff relation, unset if entity
	entity-or-relation	of EntityType : Term						//EntityType() iff entity, RelationType() iff relation
	derivation-type 		of Attribute	: DerivationType	//either is Derivation(), has DefaultValue() or is Normal()
	fulfill-role 				of EntityType : EntityType			//points to the Relation of which it is a role (the relation is EntityType)
	multiplicity				of Attribute	: Term
	
binding rules

	// modules
	ModuleDesugared(m, _, _, _) : 
		defines Module m
		scopes EntityType
		scopes Entity


	// entity types
	EntityTypeDef(ent-or-rel, e-ty, _) :
		defines EntityType e-ty
			of entity-or-relation ent-or-rel
		scopes Attribute
		scopes Role
		scopes This
		implicitly defines This This()
			of type e-ty
		
	EntityType(e) :
		refers to EntityType e
		
	
	// entities
	EntityOrRelation(EntityType(e-ty), e, _, _) :
		defines Entity e
			of type e-ty
			of multiplicity One()
		scopes This
		implicitly defines This This()
			of type e-ty
			of multiplicity One()
	
	
	// entities or attributes
	Identifier(x) :
		refers to Entity x
	otherwise
		refers to Attribute x
		in EntityType e-ty
		where This() resolves to This this
		and this has type e-ty
	
	
	// attributes
	Attribute(a, a-ty, a-mu, None()) :
		defines Attribute a
			of type a-ty
			of multiplicity a-mu
			of derivation-type Normal()
			
	Attribute(a, a-ty, a-mu, Derivation(_, a-dt)) :
		defines Attribute a
			of type a-ty
			of multiplicity a-mu
			of derivation-type a-dt
		
	AttributeName(exp, a) :
		refers to Attribute a in EntityType e-ty
		where exp has type e-ty

	
	// roles
	Role(EntityType(role-ty), role-mu, role, inverseName) :
		defines Role role
			of type role-ty
			of multiplicity role-mu
	
	RoleName(r):
		refers to Role r in EntityType e-ty
		where This() resolves to This this
		and this has type e-ty
		

	// navigators
	NavigateIn(_, _, in2, EntityType(r-ty)) :
		implicitly defines This This()
			of type r-ty in in2
		
	NavigateOut(_, _, EntityType(r-ty), out) :
		implicitly defines This This()
			of type r-ty in out
	
	
	// this
	This() :
		refers to This This()
