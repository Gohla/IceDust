module trans/naming/names

imports // constructors

	src-gen/signatures/Data-sig
	src-gen/signatures/Execute-sig
	src-gen/signatures/Expressions-sig
	src-gen/signatures/Model-sig
	src-gen/signatures/Modules-sig
	src-gen/signatures/Rules-sig
	src-gen/signatures/Types-sig
	trans/api/constructors
	trans/desugaring/constructors
	
// imports // use custom runtime libraries
// 
//   lib/nabl/-
//   lib/task/-
//   lib/properties/-
//   lib/types/-
//   lib/relations/-
//   lib/editor/-
	
namespaces
	
	Module
	Entity
	Entity2                            //Nasty Hack to enforce no duplicate entity types
	EntityInstance
	Attribute
	Role
	Inverse
	Shortcut
	This
	Rule
	LHSVariable
	NHSVariable
	RHSVariable
	ThisForAttr

properties

	derivation-type 		 of Attribute	: DerivationType	//Derivation(), DefaultValue() or Normal()
	multiplicity				 of Attribute	: Multiplicity
	multiplicity-inverse of Role      : Multiplicity
	origin-role					 of Inverse		: Role
	origin-role2				 of Shortcut	: Role
	
binding rules // modules

	ModuleDesugared(m, _, _, _, _) : 
		defines Module m
		scopes Entity
		scopes Entity2
		scopes EntityInstance
		scopes Rule

binding rules // model

	Entity(ent-or-rel, e-ty, _) :
		defines Entity2 e-ty             //Nasty Hack to enforce no duplicate entity types
		defines non-unique Entity e-ty
		scopes Attribute
		scopes Role
		scopes This
		implicitly defines This This()
			of type e-ty

	Attribute(a, a-ty, a-mu, None()) :
		defines Attribute a
			of type a-ty
			of multiplicity a-mu
			of derivation-type Normal()
			
	Attribute(a, a-ty, a-mu, Derivation(_, a-dt)) :
		defines Attribute a
			of type a-ty
			of multiplicity a-mu
			of derivation-type a-dt

	Role(NaBLHelp(role-ty, role-ty2), role-mu, role, inverseName) :
		refers to Entity2 role-ty //triggers unresolved reference errors, but provides no resolving
		defines Role role
			of type role-ty2
			of multiplicity One()
			of multiplicity-inverse role-mu
		defines non-unique Entity role-ty2
		  scopes Inverse
		defines Inverse inverseName
			of multiplicity role-mu
			of type e-ty
			of origin-role role
			where This() resolves to This this
			and this has type e-ty

	Shortcut(NaBLHelp(RoleRef(role1), type1), shortcut1, NaBLHelp(RoleRef(role2), type2), shortcut2):
		defines non-unique Entity type1
			scopes Shortcut
		defines Shortcut shortcut1
			of type type2
			of multiplicity mu1
			of origin-role role1
			of origin-role2 role2
			where role1 resolves to Role role1def
			and role1def has multiplicity-inverse mu1
		defines non-unique Entity type2
			scopes Shortcut
		defines Shortcut shortcut2
			of type type1
			of multiplicity mu2
			of origin-role role2
			of origin-role2 role1
			where role2 resolves to Role role2def
			and role2def has multiplicity-inverse mu2

binding rules // rules

	Rule(rule, params, body):
		defines Rule rule
		scopes LHSVariable
		scopes NHSVariable
		scopes RHSVariable

	//TODO: make 1 binding rule for Params
	Param(v, EntityRef(t), m):
		defines LHSVariable v
			of type t
			of multiplicity m

	Param(v, t@Int(), m):
		defines LHSVariable v
			of type t
			of multiplicity m

	Param(v, t@String(), m):
		defines LHSVariable v
			of type t
			of multiplicity m

	Param(v, t@Boolean(), m):
		defines LHSVariable v
			of type t
			of multiplicity m

	// define Node with its type from type annotation
	LHSNodeDefByType(v, EntityRef(t), a):
		defines LHSVariable v
			of type t
		scopes ThisForAttr
		implicitly defines ThisForAttr This()
			of type t
	// define Node with no type
	LHSNodeDefByType(v, t@None(), a):
		defines LHSVariable v
			of type t
		scopes ThisForAttr
		implicitly defines ThisForAttr This()
			of type t
	// define Node with its type from relation
	LHSEdge(e, LHSNodeDefByRel(v, none, a)):
		defines LHSVariable v
			of type t
			where e has type t
		scopes ThisForAttr
		implicitly defines ThisForAttr This()
			of type t

	// define Node with its type from type annotation
	NHSNodeDefByType(v, EntityRef(t), a):
		defines NHSVariable v
			of type t
		scopes ThisForAttr
		implicitly defines ThisForAttr This()
			of type t
	// define Node with no type
	NHSNodeDefByType(v, t@None(), a):
		defines NHSVariable v
			of type t
		scopes ThisForAttr
		implicitly defines ThisForAttr This()
			of type t
	// define Node with its type from relation
	NHSEdge(e, NHSNodeDefByRel(v, none, a)):
		defines NHSVariable v
			of type t
			where e has type t
		scopes ThisForAttr
		implicitly defines ThisForAttr This()
			of type t

	// define Node with its type from type annotation
	RHSNodeDefByType(v, EntityRef(t), a):
		defines RHSVariable v
			of type t
		scopes ThisForAttr
		implicitly defines ThisForAttr This()
			of type t
	// define Node with no type
	RHSNodeDefByType(v, t@None(), a):
		defines RHSVariable v
			of type t
		scopes ThisForAttr
		implicitly defines ThisForAttr This()
			of type t
	// define Node with its type from relation
	RHSEdge(e, RHSNodeDefByRel(v, none, a)):
		defines RHSVariable v
			of type t
			where e has type t
		scopes ThisForAttr
		implicitly defines ThisForAttr This()
			of type t
			
	// define LHSVariable from attribute
	AttrBindVar(a, v):
		defines LHSVariable v
			of type t
			of multiplicity m
		where a has type t
		and a has multiplicity m

	// define variables from edges
	LHSTsHelp(a, b, LHSEdgeName(NaBLHelp(v, Def()))):
		defines LHSVariable v
	NHSTsHelp(a, b, NHSEdgeName(NaBLHelp(v, Def()))):
		defines NHSVariable v
	RHSTsHelp(a, b, RHSEdgeName(NaBLHelp(v, Def()))):
		defines RHSVariable v
	
	// LHSEdge(LHSTsHelp(LHSNode(NaBLHelp(node1, a), b, c), edge, NaBLHelp(v, Def())), LHSNode(NaBLHelp(node2, d), e, f)):
	// 	defines LHSVariable v
	// 		// of type (node1, edge, node2)
	// NHSEdge(NHSTsHelp(NHSNode(NaBLHelp(node1, a), b, c), edge, NaBLHelp(v, Def())), NHSNode(NaBLHelp(node2, d), e, f)):
	// 	defines NHSVariable v
	// 		// of type (node1, edge, node2)
	// RHSEdge(RHSTsHelp(RHSNode(NaBLHelp(node1, a), b, c), edge, NaBLHelp(v, Def())), RHSNode(NaBLHelp(node2, d), e, f)):
	// 	defines RHSVariable v
	// 		// of type (node1, edge, node2)

binding rules // data

	EntityInstance(EntityRef(e-ty), e, _, _) :
		defines EntityInstance e
			of type e-ty
			of multiplicity One()
		scopes This
		implicitly defines This This()
			of type e-ty
			of multiplicity One()

binding rules // references

	Ref(x) :
		refers to EntityInstance x
	otherwise
		refers to Attribute x	in Entity e-ty
		where This() resolves to This this
		and this has type e-ty
	otherwise
	  refers to Role x in Entity e-ty
		where This() resolves to This this
		and this has type e-ty
	otherwise
	  refers to Inverse x in Entity e-ty
		where This() resolves to This this
		and this has type e-ty
	otherwise
	  refers to Shortcut x in Entity e-ty
		where This() resolves to This this
		and this has type e-ty
	otherwise
		refers to LHSVariable x

	MemberAccess(exp, a) :
		refers to Attribute a in Entity e-ty
		where exp has type e-ty
	otherwise
	  refers to Role a in Entity e-ty
	  where exp has type e-ty
	otherwise
	  refers to Inverse a in Entity e-ty
	  where exp has type e-ty
	otherwise
	  refers to Shortcut a in Entity e-ty
	  where exp has type e-ty

	EntityRef(e) :
		refers to Entity e
	
	AttributeRef(a):
		refers to Attribute a in Entity e-ty
		where This() resolves to This this
		and this has type e-ty
	
	RoleRef(r):
		refers to Role r in Entity e-ty
		where This() resolves to This this
		and this has type e-ty

	EntityInstanceRef(e) :
		refers to EntityInstance e
	
	This() :
		refers to This This()

	LHSNode(v, none, a):
		refers to LHSVariable v
		implicitly defines ThisForAttr This()
			of type t
			in a
		where v resolves to LHSVariable v-def
		and v-def has type t
		
	NHSNode(v, none, a):
		refers to LHSVariable v
		otherwise refers to NHSVariable v
		implicitly defines ThisForAttr This()
			of type t
			in a
		where v resolves to LHSVariable v-def
		and v-def has type t
		
	RHSNode(v, none, a):
		refers to LHSVariable v
		otherwise refers to RHSVariable v
		implicitly defines ThisForAttr This()
			of type t
			in a
		where v resolves to LHSVariable v-def
		and v-def has type t

	LHSEdgeName(NaBLHelp(v, Use())):
		refers to LHSVariable v
		
	NHSEdgeName(NaBLHelp(v, Use())):
		refers to LHSVariable v
		otherwise refers to NHSVariable v

	RHSEdgeName(NaBLHelp(v, Use())):
		refers to LHSVariable v
		otherwise refers to RHSVariable v

	LHSTsHelp(v1, Edge, Edge-name):
		refers to Role Edge in Entity v1-ty
		where v1 has type v1-ty
	otherwise
		refers to Inverse Edge in Entity v1-ty
		where v1 has type v1-ty
	otherwise
		refers to Shortcut Edge in Entity v1-ty
		where v1 has type v1-ty
	NHSTsHelp(v1, Edge, Edge-name):
		refers to Role Edge in Entity v1-ty
		where v1 has type v1-ty
	otherwise
		refers to Inverse Edge in Entity v1-ty
		where v1 has type v1-ty
	otherwise
		refers to Shortcut Edge in Entity v1-ty
		where v1 has type v1-ty
	RHSTsHelp(v1, Edge, Edge-name):
		refers to Role Edge in Entity v1-ty
		where v1 has type v1-ty
	otherwise
		refers to Inverse Edge in Entity v1-ty
		where v1 has type v1-ty
	otherwise
		refers to Shortcut Edge in Entity v1-ty
		where v1 has type v1-ty

	AttrRef(a):
		refers to Attribute a in Entity e-ty
		where This() resolves to ThisForAttr this
		and this has type e-ty
		
	CallRule(r,args):
		refers to Rule r

binding rules

	SubRule(a,b):
		scopes LHSVariable
		scopes NHSVariable
		scopes RHSVariable


