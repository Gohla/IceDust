module naming/names

imports // constructors

	signatures/Data-sig
	signatures/Execute-sig
	signatures/Expressions-sig
	signatures/Model-sig
	signatures/Modules-sig
	signatures/Types-sig
	api/constructors
	desugaring/constructors
	
// imports // use custom runtime libraries
// 
//   lib/nabl/-
//   lib/task/-
//   lib/properties/-
//   lib/types/-
//   lib/relations/-
//   lib/editor/-
	
namespaces
	
	Module
	Entity
	Entity2                            //Nasty Hack to enforce no duplicate entity types
	EntityInstance
	Attribute
	Role
	Inverse
	Shortcut
	This
  MemberType

properties

	derivation-type 		 of Attribute	: DerivationType	//Derivation(), DefaultValue() or Normal()
	multiplicity				 of Attribute	: Multiplicity
	multiplicity-inverse of Role      : Multiplicity
	ordering             of Role      : Ordering
	ordering-inverse     of Role      : Ordering
	inverse   					 of Role  		: Inverse
	origin-role					 of Inverse		: Role
	origin-role2				 of Shortcut	: Role
	rel-ty               of Shortcut  : Entity
	
binding rules // modules

	ModuleDesugared(m, _, _, _) : 
		defines Module m
		scopes Entity
		scopes Entity2
		scopes EntityInstance

binding rules // model

	Entity(ent-or-rel, e-ty, _) :
		defines Entity2 e-ty             //Nasty Hack to enforce no duplicate entity types
		defines non-unique Entity e-ty
		scopes Attribute
		scopes Role
		scopes This
		implicitly defines This This()
			of type e-ty

	Attribute(a, a-ty, a-mu, None()) :
		defines Attribute a
			of type a-ty
			of multiplicity a-mu
			of ordering Ordered()
			of derivation-type Normal()
			
	Attribute(a, a-ty, a-mu, Derivation(_, a-dt)) :
		defines Attribute a
			of type a-ty
			of multiplicity a-mu
			of ordering Ordered()
			of derivation-type a-dt

	Role(NaBLHelp(role-ty, role-ty2), role-mu, role-ord, role, inverseName) :
		refers to Entity2 role-ty //triggers unresolved reference errors, but provides no resolving
		defines Role role
			of type role-ty2
			of multiplicity One()
			of multiplicity-inverse role-mu
			of ordering Ordered()
			of ordering-inverse role-ord
			of inverse inverseName
		defines non-unique Entity role-ty2
		  scopes Inverse
		defines Inverse inverseName
			of multiplicity role-mu
      of multiplicity-inverse One()
			of ordering role-ord
      of ordering-inverse Ordered()
			of type this-ty
			of origin-role role
			where This() resolves to This this
			and this has type this-ty

	Shortcut(NaBLHelp(RoleRef(role1), type1), shortcut1, NaBLHelp(RoleRef(role2), type2), shortcut2):
		defines non-unique Entity type1
			scopes Shortcut
		defines Shortcut shortcut1
			of type type2
			of multiplicity mu1
			of ordering or1
			of origin-role role1
			of origin-role2 role2
//			of rel-ty rel-ty
			where role1 resolves to Role role1def
			and role1def has multiplicity-inverse mu1
			and role1def has ordering-inverse or1
		defines non-unique Entity type2
			scopes Shortcut
		defines Shortcut shortcut2
			of type type1
			of multiplicity mu2
			of ordering or2
			of origin-role role2
			of origin-role2 role1
//			of rel-ty rel-ty
			where role2 resolves to Role role2def
			and role2def has multiplicity-inverse mu2
			and role2def has ordering-inverse or2
//      and This() resolves to This this
//      and this has type rel-ty

binding rules // data

	EntityInstance(e, EntityRef(e-ty), _) :
		defines EntityInstance e
			of type e-ty
			of multiplicity One()
			of ordering Ordered()
		scopes This
		implicitly defines This This()
			of type e-ty
			of multiplicity One()
			of ordering Ordered()
			
    EntityInstance(e, None(), _) :
    defines EntityInstance e
      of type e-ty
      of multiplicity One()
      of ordering Ordered()
      where MemberType() resolves to MemberType membertype
      and membertype has type e-ty
    scopes This
    implicitly defines This This()
      of type e-ty
      of multiplicity One()
      of ordering Ordered()

  MemberValue(m, value) :
    scopes MemberType
    implicitly defines MemberType MemberType()
      of type m-ty
      where m has type m-ty

binding rules // references

	Ref(x) :
		refers to EntityInstance x
	otherwise
		refers to Attribute x	in Entity e-ty
		where This() resolves to This this
		and this has type e-ty
	otherwise
	  refers to Role x in Entity e-ty
		where This() resolves to This this
		and this has type e-ty
	otherwise
	  refers to Inverse x in Entity e-ty
		where This() resolves to This this
		and this has type e-ty
	otherwise
	  refers to Shortcut x in Entity e-ty
		where This() resolves to This this
		and this has type e-ty

	MemberAccess(exp, a) :
		refers to Attribute a in Entity e-ty
		where exp has type e-ty
	otherwise
	  refers to Role a in Entity e-ty
	  where exp has type e-ty
	otherwise
	  refers to Inverse a in Entity e-ty
	  where exp has type e-ty
	otherwise
	  refers to Shortcut a in Entity e-ty
	  where exp has type e-ty

	EntityRef(e) :
		refers to Entity e
  
  AttributeRef(a):
    refers to Attribute a in Entity e-ty
    where This() resolves to This this
    and this has type e-ty
	
	RoleRef(r):
		refers to Role r in Entity e-ty
		where This() resolves to This this
		and this has type e-ty

  MemberRef(m):
    refers to Attribute m in Entity e-ty
    where This() resolves to This this
    and this has type e-ty
  otherwise
    refers to Shortcut m in Entity e-ty
    where This() resolves to This this
    and this has type e-ty

	EntityInstanceRef(e) :
		refers to EntityInstance e
	
	This() :
		refers to This This()


