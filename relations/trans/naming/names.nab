module trans/naming/names

imports

	include/Relations
	trans/desugaring/desugar
	trans/naming/_notNaBL
	
	lib/nabl/-
	
namespaces
	
	Module
	Entity
	Entity2                            //Nasty Hack to enforce no duplicate entity types
	EntityInstance
	Attribute
	Role
	Inverse
	Shortcut
	This
	Rule
	Variable
	ThisForAttr

properties

	derivation-type 		 of Attribute	: DerivationType	//Derivation(), DefaultValue() or Normal()
	multiplicity				 of Attribute	: Multiplicity
	multiplicity-inverse of Role      : Multiplicity
	origin-role					 of Inverse		: Role
	origin-role2				 of Shortcut	: Role
	
binding rules // modules

	ModuleDesugared(m, _, _, _, _) : 
		defines Module m
		scopes Entity
		scopes Entity2
		scopes EntityInstance
		scopes Rule

binding rules // model

	Entity(ent-or-rel, e-ty, _) :
		defines Entity2 e-ty             //Nasty Hack to enforce no duplicate entity types
		defines non-unique Entity e-ty
		scopes Attribute
		scopes Role
		scopes This
		implicitly defines This This()
			of type e-ty

	Attribute(a, a-ty, a-mu, None()) :
		defines Attribute a
			of type a-ty
			of multiplicity a-mu
			of derivation-type Normal()
			
	Attribute(a, a-ty, a-mu, Derivation(_, a-dt)) :
		defines Attribute a
			of type a-ty
			of multiplicity a-mu
			of derivation-type a-dt

	Role(NaBLHelp(role-ty, role-ty2), role-mu, role, inverseName) :
		refers to Entity2 role-ty //triggers unresolved reference errors, but provides no resolving
		defines Role role
			of type role-ty2
			of multiplicity One()
			of multiplicity-inverse role-mu
		defines non-unique Entity role-ty2
		  scopes Inverse
		defines Inverse inverseName
			of multiplicity role-mu
			of type e-ty
			of origin-role role
			where This() resolves to This this
			and this has type e-ty

	Shortcut(NaBLHelp(RoleRef(role1), type1), shortcut1, NaBLHelp(RoleRef(role2), type2), shortcut2):
		defines non-unique Entity type1
			scopes Shortcut
		defines Shortcut shortcut1
			of type type2
			of multiplicity mu1
			of origin-role role1
			of origin-role2 role2
			where role1 resolves to Role role1def
			and role1def has multiplicity-inverse mu1
		defines non-unique Entity type2
			scopes Shortcut
		defines Shortcut shortcut2
			of type type1
			of multiplicity mu2
			of origin-role role2
			of origin-role2 role1
			where role2 resolves to Role role2def
			and role2def has multiplicity-inverse mu2

binding rules // rules

	Rule(rule, params, body):
		defines Rule rule
		scopes Variable

	//TODO: make 1 binding rule for Params
	Param(v, EntityRef(t), m):
		defines Variable v
			of type t
			of multiplicity m

	Param(v, t@Int(), m):
		defines Variable v
			of type t
			of multiplicity m

	Param(v, t@String(), m):
		defines Variable v
			of type t
			of multiplicity m

	Param(v, t@Boolean(), m):
		defines Variable v
			of type t
			of multiplicity m

	// define node with its type from type annotation
	Node(NaBLHelp(v, Def()), EntityRef(t), a):
		defines Variable v
			of type t
		scopes ThisForAttr
		implicitly defines ThisForAttr This()
			of type t
			
	Node(NaBLHelp(v, Def()), t@None(), a):
		defines Variable v
			of type t
		scopes ThisForAttr
		implicitly defines ThisForAttr This()
			of type t
	
	// define node with its type from relation
	Edge(e, Node(NaBLHelp(v, DefByRel()), none, a)):
		defines Variable v
			of type t
			where e has type t
		scopes ThisForAttr
		implicitly defines ThisForAttr This()
			of type t
			
	// define variable from attribute
	Attr(NaBLHelp(a, Bind()), v):
		defines Variable v
			of type t
			of multiplicity m
		where a has type t
		and a has multiplicity m

binding rules // data

	EntityInstance(EntityRef(e-ty), e, _, _) :
		defines EntityInstance e
			of type e-ty
			of multiplicity One()
		scopes This
		implicitly defines This This()
			of type e-ty
			of multiplicity One()

binding rules // references

	Ref(x) :
		refers to EntityInstance x
	otherwise
		refers to Attribute x	in Entity e-ty
		where This() resolves to This this
		and this has type e-ty
	otherwise
	  refers to Role x in Entity e-ty
		where This() resolves to This this
		and this has type e-ty
	otherwise
	  refers to Inverse x in Entity e-ty
		where This() resolves to This this
		and this has type e-ty
	otherwise
	  refers to Shortcut x in Entity e-ty
		where This() resolves to This this
		and this has type e-ty
	otherwise
		refers to Variable x

	MemberAccess(exp, a) :
		refers to Attribute a in Entity e-ty
		where exp has type e-ty
	otherwise
	  refers to Role a in Entity e-ty
	  where exp has type e-ty
	otherwise
	  refers to Inverse a in Entity e-ty
	  where exp has type e-ty
	otherwise
	  refers to Shortcut a in Entity e-ty
	  where exp has type e-ty

	EntityRef(e) :
		refers to Entity e
	
	AttributeRef(a):
		refers to Attribute a in Entity e-ty
		where This() resolves to This this
		and this has type e-ty
	
	RoleRef(r):
		refers to Role r in Entity e-ty
		where This() resolves to This this
		and this has type e-ty

	EntityInstanceRef(e) :
		refers to EntityInstance e
	
	This() :
		refers to This This()

	Node(NaBLHelp(v, Use()), none, a):
		refers to Variable v
		implicitly defines ThisForAttr This()
			of type t
			in a
		where v resolves to Variable v-def
		and v-def has type t

	TsHelp(v1, edge):
		refers to Role edge in Entity v1-ty
		where v1 has type v1-ty
	otherwise
		refers to Inverse edge in Entity v1-ty
		where v1 has type v1-ty
	otherwise
		refers to Shortcut edge in Entity v1-ty
		where v1 has type v1-ty

	AttrRef(a):
		refers to Attribute a in Entity e-ty
		where This() resolves to ThisForAttr this
		and this has type e-ty
