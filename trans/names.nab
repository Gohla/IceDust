module names

imports
	
	include/Relations
	
namespaces
	
	Module
	EntityType
	Entity
	Attribute
	Role

	
binding rules

	// modules
	Module(m, _, _, _) : 
		defines Module m
		scopes EntityType


	// entity types
	EntityTypeDef(e, _) :
		defines EntityType e of type Type(e)
		scopes Attribute
	RelationTypeDef(e, _) :
		defines EntityType e of type Type(e)
		scopes Role
		scopes Attribute
		
	EntityType(e) :
		refers to EntityType e
		
	
	// entities
	EntityOrRelation(entityType, e, _, _) :
		defines Entity e of type entityType
	
	EntityName(e) :
		refers to Entity e
	
	
	// attributes
	Attribute(a, t) :
		defines Attribute a of type t
		
	AttributeName(EntityType(e), a) :
		refers to Attribute a in EntityType e
	
	// roles
	Role(roleType, multiplicity, role) :
		defines Role role of type roleType //ignoring multiplicties in types atm
	
	RoleName(EntityType(e), r) :
		refers to Role r in EntityType e
		




	
