module names

imports
	
	include/Relations
	
namespaces
	
	Module
	EntityType
	Entity
	Attribute
	Role
	This

	
binding rules

	// modules
	Module(m, _, _, _) : 
		defines Module m
		scopes EntityType
		scopes Entity


	// entity types
	EntityTypeDef(e, _) :
		defines EntityType e //of type Type(e)
		scopes Attribute
	RelationTypeDef(e, _) :
		defines EntityType e //of type Type(e)
		scopes Role
		scopes Attribute
		
	EntityType(e) :
		refers to EntityType e
		
	
	// entities
	EntityOrRelation(EntityType(entityType), e, _, _) :
		defines Entity e of type entityType
		scopes This
		implicitly defines This "this" of type entityType								//Post p1 {title="asdf"} works like Post p1 {this.title="asdf"}
	
	EntityName(e) :
		refers to Entity e
	
	
	// attributes
	Attribute(a, t) :
		defines Attribute a of type t
		
	AttributeNameUnqualified(a):														//Post p1 {title="asdf"} works like Post p1 {this.title="asdf"}
		refers to Attribute a in EntityType t
		where This "this" has type t
		
	AccessEntityAttribute(EntityName(e), a) :
		refers to Attribute a in EntityType t
		where Entity e has type t
	
	// roles
	Role(roleType, multiplicity, role) :
		defines Role role of type roleType //ignoring multiplicties in types atm
		//TODO: implicitly defines a "RoleInRelation" relation in "EntityType" roleType, where relation is the parent of Role
	
	RoleNameUnqualified(r):																//Post p1 {poster:me} works like Post p1 {this.poster:me}
		refers to Role r in EntityType t
		where This "this" has type t
		




	
